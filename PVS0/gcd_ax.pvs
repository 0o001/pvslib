gcd_ax : THEORY
BEGIN
  MT: TYPE+ % abstract type for measure

  wfm_gcd: [[nat,nat] -> MT] % the measure function has to be defined on both parameters of gcd.

  R: pred[[MT,MT]]   % relation to compare measures

  R_wf: AXIOM % for gcd_TCC1
    well_founded?(LAMBDA (x: MT, y: MT): R(x, y));

  wfm_gcd_dec_1: AXIOM % for gcd_TCC2
    FORALL (m, n: nat):
     m >= n AND NOT m = 0 AND NOT n = 0 IMPLIES
      R(wfm_gcd(m - n, n), wfm_gcd(m, n));

  wfm_gcd_dec_2: AXIOM % for gcd_TCC3
  FORALL (m, n: nat):
    NOT m >= n AND NOT m = 0 AND NOT n = 0 IMPLIES
     R(wfm_gcd(n, m), wfm_gcd(m, n));

  gcd(m,n:nat) : RECURSIVE nat = 
    IF m=0 OR n=0 THEN m+n
    ELSIF m >= n THEN
      gcd(m-n,n)
    ELSE gcd(n,m)
    ENDIF
  MEASURE wfm_gcd BY R

END gcd_ax
