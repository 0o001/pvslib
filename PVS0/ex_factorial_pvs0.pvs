ex_factorial_pvs0: THEORY
BEGIN
% ------------------------------------------------------------------------------ 
% Part0: Definition of factorial in PVS0 (taken from factorial.pvs)
%
  IMPORTING lang[nat]

  ebool(n:nat) : bool = n /= 0

  boole(b:bool) : nat = IF NOT b THEN 0 ELSE 1 ENDIF

  eop1(op:nat)(n:nat) : nat = 
    COND op = 0 -> boole(n = 0),
         op = 1 -> IF n = 0 THEN 0 ELSE n-1 ENDIF,
         ELSE -> n
    ENDCOND

  eop2(op:nat)(n,m:nat) : nat = n*m

  % Interpretation:
  %   vr      : n
  %   bool(n) : n /= 0
  %   op1 0   : n -> bool(n=0)
  %   op1 1   : n -> n-1 
  %   op2 0   : n,m -> n*m

  pvs0_factorial : PVS0 =
    def(ite(op1(0,vr),cnst(1),op2(0,vr,rec(op1(1,vr)))));

% ------------------------------------------------------------------------------ 
% Part1: Proof of termination via pvs0_tccs
%    
  % (This definition allows to call < the order on nats instead of the order on reals.)
  <: (well_founded?[nat]) = restrict[[real, real], [nat, nat], boolean](reals.<);

  IMPORTING measure_termination_defs
  	    [ nat   % Base type
	    , nat   % Measure type
	    , <     % Measure relation
	    ] AS mt_nat

  % This lemma assures that there are one measure type and one measure relation 
  % that can be used to prove termination via pvs0_tcc.
  pvs0_factorial_tcc_termination: LEMMA
    mt_nat.pvs0_tcc_termination(ebool,eop1,eop2)(pvs0_factorial)

% ------------------------------------------------------------------------------ 
% Part2: Any measure fulfilling pvs0_tcc can be used to prove pvs' tccs.
%
  MT_: TYPE+
  R_ : (well_founded?[MT_])

  IMPORTING measure_termination_defs
  	    [ nat   % Base type
	    , MT_   % Measure type
	    , R_    % Measure relation
	    ] AS mt_gen

  wfm_f_: mt_gen.WFM

  pvs0_factorial_pvs0_tcc_gen_termination: AXIOM
    mt_gen.pvs0_tcc_termination_(ebool,eop1,eop2)(pvs0_factorial)(wfm_f_)

  % By proving the TCCs generated by this importing clause, we are proving that any
  % measure type and any relation such that pvs0_tcc_termination is valid, can be
  % used to prove termination via tcc in pvs.
  IMPORTING ex_factorial {{ MT := MT_, R := R_ , wfm_f := wfm_f_}}

% ------------------------------------------------------------------------------ 
% Part3: Semantic equivalence between definitions.
%
  pvs0_factorial_eval : LEMMA
    FORALL(n: nat):
      LET v = eval(ebool,eop1,eop2)(n+1,pvs0_factorial)(val2env(n)) IN
      some?(v) AND factorial(n) = val(v)  

END ex_factorial_pvs0
