orders_more_props[T: TYPE+, lt: (well_ordered?[T])]: THEORY
BEGIN

  is_first_lt(x: T): bool =
    FORALL(y: T): x /= y IMPLIES lt(x, y)

  first_lt_existence: LEMMA
    EXISTS(x: T): is_first_lt(x)

  first_lt: T

  first_lt_def: AXIOM 
    is_first_lt(first_lt)

  has_prev(y: T) : bool = 
    EXISTS(x: T): lt(x, y)

  is_prev_lt(y: T | has_prev(y))(prev: T): bool =
    lt(prev,y) AND FORALL(x: T): lt(x,y) IMPLIES (lt(x,prev) OR x=prev)

  prev_lt_existence: CONJECTURE 
    FORALL(y: T): 
    has_prev(y) IMPLIES EXISTS(x: T): is_prev_lt(y)(x)

  prev_lt(y: T | has_prev(y)): T 

  prev_lt_def: AXIOM % TOO STRONG (and apparently false)
    FORALL(y: T):    % it is equivalent to Omega_is_finite
    has_prev(y) IMPLIES is_prev_lt(y)(prev_lt(y))

  nth_from(y: T)(n: nat): RECURSIVE T =
    IF(n=0 OR NOT has_prev(y))THEN y
    ELSE nth_from(prev_lt(y))(n-1)  ENDIF
    MEASURE n by <

  is_height_lt(y: T)(h: nat) : bool = (nth_from(y)(h) = first_lt)

  is_height_lt_gt0: LEMMA
    FORALL (y: (has_prev), h: nat):
      is_height_lt(y)(h) IMPLIES h>0
  
  is_height_lt_dec: LEMMA
    FORALL (y: (has_prev), h: upfrom(1)):
      is_height_lt(y)(h) IMPLIES is_height_lt(prev_lt(y))(h-1)

  height_lt(y: T): nat 
  = min(is_height_lt(y))

  height_lt_dec: LEMMA
    FORALL (y: (has_prev)):
      height_lt(prev_lt(y)) < height_lt(y)

  Omega_set(y: T): RECURSIVE { res: finite_set[T] | FORALL(x:T): res(x) IFF 
  	       	   	       	    		    x=y OR lt(x, y) } =
    IF has_prev(y)
    THEN add(y, Omega_set(prev_lt(y)))
    ELSE singleton(y) ENDIF
    MEASURE height_lt(y)
  

END orders_more_props
