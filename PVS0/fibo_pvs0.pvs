fibo_pvs0 : THEORY

  BEGIN

  IMPORTING pvs0_prelude,
            pvs0_cc[[nat,nat,nat]]

  false_val : MACRO Val = (0,0,0)
  true_val  : MACRO Val = (1,1,1)

  boole(b:bool) : MACRO Val = IF b THEN true_val ELSE false_val ENDIF

  fibo_op1(op:nat)(f0,f1,n:nat) : MACRO Val = 
    COND op = 0 -> boole(n=0),
         op = 1 -> boole(n=1),
         op = 2 -> (0,f0,0),
         op = 3 -> (0,f1,0),
         op = 4 -> IF n /= 0 THEN (f1,f0+f1,n-1) ELSE false_val ENDIF,
         ELSE -> false_val
    ENDCOND

  fibo_op2(op:nat)(v1,v2:Val) : MACRO Val = false_val

  pvs0_fibo : PVS0 = (false_val,
                      fibo_op1,fibo_op2,
   	              ite(op1(0,vr), op1(2,vr),
                          ite(op1(1,vr),op1(3,vr),
                              rec(op1(4,vr)))))

  N  : MACRO posnat = 1

  IMPORTING CCG@ccg_to_mwg[Val, PVS0Expr[Val], Conditions, Path,
             MT,lt,N,
             LAMBDA (expr: PVS0Expr[Val], env, val: Val):
             semantic_rel_expr(pvs0_fibo`1,pvs0_fibo`2,pvs0_fibo`3)
                              (expr, pvs0_fibo`4,
                               env, val),
           LAMBDA (conds: Conditions, env: Val):
             eval_conds(pvs0_fibo, conds, env)]

  M : MACRO FunMeasure = LAMBDA (v:Val) : v`3

  fibo_measures: FunMeasures = LAMBDA (n:below(N)): M

  IMPORTING ccg_to_pvs0 [Val,MT,lt,N],
  	    pvs0_eval[Val],
            CCG@bounding_circuits[CallingContext,N]

  A: Measure_Matrix = (:(:1:):)
  
  fibo_dg : (cdigraph) = pvs0_to_dg(pvs0_fibo)

  fibo_ccg: CCG = make_ccg(fibo_dg,fibo_measures)

  fibo_mm: (indi_f_sound(fibo_ccg)) =
  	LAMBDA(v:vert(dg(fibo_ccg))): A

  pvs0_fibo_terminating: JUDGEMENT
    pvs0_fibo HAS_TYPE (terminating?)

  pvs0_fibo_tcc_termination: JUDGEMENT
    pvs0_fibo HAS_TYPE (pvs0_tcc_termination)

  fibo_msr : LEMMA
    EXISTS (m:WFM): pvs0_tcc_termination_pred(pvs0_fibo)(m)
    
  fibo_wfm  : (pvs0_tcc_termination_pred(pvs0_fibo))

  fibo_pvs0(f0,f1,n:nat): MACRO nat = pvs0_eval(pvs0_fibo)(f0,f1,n)`2

END fibo_pvs0
