%*************************************************************************************%
% This theory proves that any PVS function of type [T1->T2] can be embedded as        %
% a terminating PVS0 function                                                         %
%*************************************************************************************%

pvs0_pvs[T1,T2:TYPE+] : THEORY
BEGIN

  IMPORTING pvs0_lang[[T1,T2]],
            pvs0_eval[[T1,T2]]

  FalseVal : [T1,T2]

  make_pvs0(pvs:[T1->T2]) : (terminating?) =
    (FalseVal,
     (:LAMBDA(tt:[T1,T2]):(FalseVal`1,pvs(tt`1)):),
     (:LAMBDA(t1,t2:[T1,T2]):t1:),
     op1(0,vr))

  pvs_to_pvs0_aux : LEMMA
    FORALL (pvs:[T1->T2],t1:T1) :
       pvs0_eval(make_pvs0(pvs))((t1,FalseVal`2)) = (FalseVal`1,pvs(t1))

  pvs_to_pvs0 : THEOREM
    FORALL (pvs:[T1->T2],t1:T1) : 
      pvs(t1) = pvs0_eval(make_pvs0(pvs))(t1,FalseVal`2)`2  

END pvs0_pvs
