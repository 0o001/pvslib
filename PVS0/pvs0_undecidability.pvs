%**********************************************************************************%
% This theory proves that if there is a surjective function from a non-empty       %
% type T to PVS0[T,T] and a bijective function from T to [T,T], then there is no   %
% terminating function in PVS0[T,T] that for a given funcion pvs0 in PVS0[T,T] and % 
% point t in [T,T] determines if pvs0(t) terminates.                               %
%**********************************************************************************%
pvs0_undecidability[T : TYPE+] : THEORY
BEGIN

  ASSUMING
    IMPORTING pvs0_lang[[T,T]]

    to_PVS0 : ASSUMPTION
      EXISTS (f:[T->PVS0[Val]]): surjective?(f)

    to_Val : ASSUMPTION
      EXISTS (f:[T->Val]) : surjective?(f)

  ENDASSUMING

  to_PVS0   : (surjective?[T,PVS0])

  from_PVS0 : (injective?[PVS0,T]) = inverse(to_PVS0)

  another(val:Val) : {v:Val|v /= val} = choose({v:Val|v /= val})  

  to_Val   : (surjective?[T,Val])

  from_Val : (injective?[Val,T]) = inverse(to_Val)

  pvs0_undecidability: THEOREM
    FORALL (TFalse : Val):
      NOT EXISTS (f: (terminating?)): FORALL (pvs0:PVS0,val:Val):
        semantic_rel(f,(from_PVS0(pvs0),from_Val(val)),another(TFalse)) IFF determined?(pvs0,val)

END pvs0_undecidability
