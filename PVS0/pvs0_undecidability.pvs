%********************************************************************************%
% This theory proves that if there is a surjective function from a non-empty     %
% type T to PVS0[T,T], then there is no terminating function in PVS0[T,T] that   %
% decides the diagonal of all functions in PVS0[T,T].                   	 %
%********************************************************************************%
pvs0_undecidability[T : TYPE+] : THEORY
BEGIN

  ASSUMING
    IMPORTING pvs0_lang[[T,T]]

    to_PVS0 : ASSUMPTION
      EXISTS (f:[T->PVS0[Val]]): surjective?(f)

  ENDASSUMING

  to_PVS0: (surjective?[T,PVS0])

  from_PVS0 : (injective?[PVS0,T]) = inverse(to_PVS0)
  
  TerminationUndecidable: THEOREM
    FORALL (TTrue : Val, TFalse : Val | TTrue /= TFalse) :
    NOT EXISTS (f: (terminating?)): FORALL (pvs0:PVS0,t:T):
      semantic_rel(f,(from_PVS0(pvs0),t),TTrue) IFF determined?(pvs0,(t,t))

END pvs0_undecidability
