%************************************************************************************************%
% This theory proves that the halting problem is undecidable in computable PVS0 functions, i.e., %
% There is no computable function in PVS0[nat] that decides whether or not a partial recursvie   %
% function pvs0 in PVS0[nat] terminates on an given natural number n.                            % 
%************************************************************************************************%

pvs0_halting[false_val:nat,lop1:list[[nat->nat]],lop2:list[[[nat,nat]->nat]]] : THEORY
BEGIN

  IMPORTING pvs0_computable[false_val,lop1,lop2],
  	    pvs0_eval[nat],
            structures@more_list_props

  nat_to_PVS0 : (surjective?[nat,PVS0_partial_recursive])

  PVS0_to_nat : (injective?[PVS0_partial_recursive,nat]) = inverse(nat_to_PVS0)

  true_val : {x : Val | x /= false_val}

  Oracle : (terminating?) = (false_val,
    (: LAMBDA(pb:Val) : LET (p,n) = nat2tuple(pb),
			     pvs0 = nat_to_PVS0(p) IN
			IF determined?(pvs0,n) THEN true_val ELSE false_val ENDIF :),
    (::),
    op1(0,vr))

  % The following theorem shows that Oracle is a terminating PVS0 function that decides the
  % halting problem of partial recursive functions in PVS0[nat].
  % However, that function is not computable.

  pvs0_decidability: THEOREM
    FORALL (pvs0:PVS0_partial_recursive,n:nat): 
      pvs0_eval(Oracle)(tuple2nat(PVS0_to_nat(pvs0),n)) /= false_val IFF determined?(pvs0,n)

  % The following lemma shows that the halting problem is undecidable in PVS0_computable
  pvs0_undecidability: THEOREM
    NOT EXISTS (oracle:PVS0_computable): FORALL (pvs0:PVS0_partial_recursive,n:nat): 
      pvs0_eval(oracle)(tuple2nat(PVS0_to_nat(pvs0),n)) /= false_val IFF determined?(pvs0,n)

END pvs0_halting
