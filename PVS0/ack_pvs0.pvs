ack_pvs0 : THEORY

  BEGIN

  IMPORTING pvs0_prelude,
            pvs0_cc[[nat,nat]]

  false_val : MACRO [nat,nat] = (0,0)

  boole(b:bool) : MACRO Val = IF NOT b THEN false_val ELSE (1,1) ENDIF

  ack_op1(op:nat)(m,n:nat) : Val = 
    COND op = 0 -> boole(m=0),
         op = 1 -> boole(n=0),
         op = 2 -> (n+1,0),
         op = 3 -> IF m /= 0 THEN (m-1,1) ELSE (m,n) ENDIF,
	 op = 4 -> IF n /= 0 THEN (m,n-1) ELSE (m,n) ENDIF,
         ELSE -> (m,n)
    ENDCOND

  ack_op2(op:nat)(v1,v2:Val) : Val = 
    IF v1`1 > 0 THEN (v1`1-1,v2`1) ELSE v1 ENDIF

  pvs0_ack : PVS0 = (false_val,
                     ack_op1,ack_op2,
  	             ite(op1(0,vr), op1(2,vr),
                         ite(op1(1,vr),rec(op1(3,vr)),
                             rec(op2(0,vr,rec(op1(4,vr)))))))

  N  : MACRO posnat = 2

  IMPORTING CCG@ccg_to_mwg[Val, PVS0Expr[Val], Conditions, Path,
             MT,lt,N,
             LAMBDA (expr: PVS0Expr[Val], env, val: Val):
             semantic_rel_expr(pvs0_ack`1,pvs0_ack`2,pvs0_ack`3)
                              (expr, pvs0_ack`4,
                               env, val),
           LAMBDA (conds: Conditions, env: Val):
             eval_conds(pvs0_ack, conds, env)]

  M1 : MACRO FunMeasure = LAMBDA (v:Val) : v`1
  M2 : MACRO FunMeasure = LAMBDA (v:Val) : v`2

  ack_measures: FunMeasures = 
     LAMBDA (i:below(N)): 
       IF i = 0 THEN M1
       ELSE M2
       ENDIF 

  IMPORTING ccg_to_pvs0 [[nat,nat],MT,lt,N],
  	    scp_iff_pvs0[[nat,nat]],
            CCG@bounding_circuits[CallingContext,N]

  % Matrices taken from Andrea's slides
  
  A0: Measure_Matrix = (: (:  1 ,  0 :),
      		          (: -1 , -1 :) :)
  
  A1: Measure_Matrix = (: (:  1 , -1 :),
      		          (: -1 , -1 :) :)

  A2: Measure_Matrix = (: (:  0 , -1 :),
      		          (: -1 , 1 :) :)
 
  ack_dg : (cdigraph) = pvs0_to_dg(pvs0_ack)

  ack_ccg: CCG = make_ccg(ack_dg,ack_measures)

  ack_mm: (indi_f_sound(ack_ccg)) =
   LAMBDA(v: vert(dg(ack_ccg))):
     IF v`nid = (: 1, 2 :) THEN A0
     ELSIF v`nid =  (: 2, 2 :) THEN A1
     ELSE A2
     ENDIF

  ack_terminates: LEMMA terminating?(pvs0_ack)

  ack_wgd: wdg = ccg_to_mwg(ack_ccg)(ack_mm)

  pvs0_ack_tcc_termination: JUDGEMENT
    pvs0_ack HAS_TYPE (pvs0_tcc_termination)

  ack_msr : LEMMA
    EXISTS (m:WFM): pvs0_tcc_termination_pred(pvs0_ack)(m)
    
  ack_wfm  : (pvs0_tcc_termination_pred(pvs0_ack))

  ack_pvs0(m,n:nat): nat = pvs0_eval(pvs0_ack)(m,n)`1

  ack_pvs0_m_n : LEMMA
    FORALL (m:nat,n:nat): ack_pvs0(m,n) = 
      IF m = 0 THEN n+1
      ELSIF n = 0 THEN ack_pvs0(m-1,1)
      ELSE ack_pvs0(m-1,ack_pvs0(m,n-1))
      ENDIF

END ack_pvs0
