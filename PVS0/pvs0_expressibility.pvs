%********************************************************************************%
% This theory proves that there is a terminating function in PVS0[PVS[T]] that   %
% decides termination of all functions in PVS0[T]. That function, however, is    %
% not computable.                                               	         %
%********************************************************************************%
pvs0_expressibility[T : TYPE+] : THEORY
BEGIN

  IMPORTING pvs0_lang,
            pvs0_pvs_T[PVS0[T]],
	    pvs0_eval[PVS0[T]]

  Oracle(false_val: PVS0[T], true_val:PVS0[T] | true_val /= false_val): (terminating?[PVS0[T]]) =
    make_pvs0(LAMBDA(pvs0:PVS0[T]):IF terminating?(pvs0) THEN true_val ELSE false_val ENDIF)

  TerminationExpressible : THEOREM
    FORALL (pvs0:PVS0[T], false_val: PVS0[T], true_val:PVS0[T] | true_val /= false_val) : 
        pvs0_eval(Oracle(false_val,true_val))(pvs0) = true_val IFF terminating?(pvs0)

END pvs0_expressibility
