measure_termination_defs[ T  : TYPE+  % base type
                        , MT : TYPE   % measure type
		        , lt : (well_founded?[MT])
                        ] : THEORY
BEGIN
  IMPORTING pvs0_props[T]

  WFM : TYPE =  [Val -> MT]
  
  pvs0: VAR PVS0  
  
  %% Measure termination in PVS0

  pvs0_tcc_termination_pred(pvs0)(wfm:WFM) : bool =
   FORALL (env:Val) (cc : (pvs0_tcc_valid_cc(pvs0`4))) (vact : Val):  
    semantic_rel_expr(pvs0`1,pvs0`2,pvs0`3)
                     (get_arg(cc`rec_expr),pvs0`4,env,vact) AND
    eval_conds(pvs0,cc`cnds,env)  
    IMPLIES lt(wfm(vact), wfm(env))
  
  pvs0_tcc_termination(pvs0) : bool =
    EXISTS (wfm: WFM):
      pvs0_tcc_termination_pred(pvs0)(wfm)

END measure_termination_defs
