measure_termination_defs[ Val : TYPE+  % base type
                        , MT  : TYPE   % measure type
		        , lt  : (well_founded?[MT])
                        ] : THEORY
BEGIN

IMPORTING pvs0_props[Val]

  WFM : TYPE =  [Val -> MT]
  
  pvs0: VAR PVS0  
  
  %% Measure termination in PVS0

  pvs0_tcc_termination_pred(pvs0)(wfm:WFM) : bool =
   FORALL (env:Val) (cc : (pvs0_tcc_valid_cc(get_body(pvs0)))) (vact : Val):  
    semantic_rel_expr(pvs0)(get_arg(cc`rec_expr),env,vact) AND
    eval_conds(pvs0)(cc`cnds,env)  
    IMPLIES lt(wfm(vact),wfm(env))
  
  pvs0_tcc_termination(pvs0) : bool =
    EXISTS (wfm: WFM):
      pvs0_tcc_termination_pred(pvs0)(wfm)

  wfm_tcc(pvs0:(pvs0_tcc_termination)) : (pvs0_tcc_termination_pred(pvs0))

END measure_termination_defs
