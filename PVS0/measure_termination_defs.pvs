measure_termination_defs[ T  : TYPE+  % base type
                        , MT : TYPE   % measure type
		        , lt : (well_founded?[MT])
                        ] : THEORY
BEGIN
  IMPORTING pvs0_props[T]

  WFM : TYPE =  [Val -> MT]
  
  eval_bool : VAR [Val -> bool] 
  eval_op1  : VAR [nat->[Val->Val]]
  eval_op2  : VAR [nat->[[Val,Val]->Val]]
  
  %% Measure termination in PVS0  

  pvs0_tcc_termination_pred(eval_bool,eval_op1,eval_op2)(def:Def)(wfm:WFM) : bool =
   FORALL (env:Environment) (cc : (pvs0_tcc_valid_cc(def))) (vact : Val):  
    semantic_rel_expr(eval_bool,eval_op1,eval_op2)(get_arg(cc`rec_expr),get_body(def),env,vact) AND
    eval_conds_rec(eval_bool,eval_op1,eval_op2)(get_body(def),cc`cnds,env)  
    IMPLIES lt(wfm(vact), wfm(env(vr)))

  pvs0_tcc_termination(eval_bool,eval_op1,eval_op2)(def:Def) : bool =
    EXISTS (wfm: WFM): pvs0_tcc_termination_pred(eval_bool,eval_op1,eval_op2)(def)(wfm)

END measure_termination_defs
