%********************************************************************************%
%   SCP if and only if PVS0 termination  					 %
%		  	      	      	      	       	      			 %
%   Authors:									 %
%   										 %
%       Andréia Borges Avelar -- Universidade  de  Brasília  - Brasil  		 %
%       Mauricio Ayala-Rincon -- Universidade  de  Brasília  - Brasil  		 %
%                 Cesar Muñoz -- NASA  Langley  Research  Center - US		 %
%	   Mariano M. Moscato -- National Institute of Aerospace - US            %
%		  	      	      	      	       	      			 %
%********************************************************************************%
scp_iff_pvs0[Val:TYPE+] : THEORY
BEGIN

  IMPORTING pvs0_to_dg[Val]
  	    
  pvs0: VAR PVS0

  IMPORTING CCG@scp

  scp_termination_pvs0(pvs0) : bool =
    scp_termination?[Val,PVS0Expr,Conditions,Path,[nat->(cc_of_pvs0(get_body(pvs0)))]]
                    (semantic_rel_expr(pvs0),eval_conds(pvs0))

  x,y: VAR Val 

  R(pvs0)(x,y): bool = 
    EXISTS(cc: (cc_of_pvs0(get_body(pvs0)))):
      eval_conds(pvs0)(cc`conds,y) AND
      semantic_rel_expr(pvs0)(cc`actuals,y,x)

  IMPORTING orders@well_foundedness[Val]

  R_wf: LEMMA
    scp_termination_pvs0(pvs0) IMPLIES well_founded?(R(pvs0))

  IMPORTING structures@more_list_props

  R_no_desc_inf_branch: LEMMA
    FORALL (x: Val): is_finite({y: Val | R(pvs0)(y, x)})

  IMPORTING measure_termination[Val],
            pvs0_termination[Val,MT,lt]

% ------------------------------------------------------------------------------- %
% SCP implies PVS0 measure termination

  scp_implies_pvs0_tcc : LEMMA
    scp_termination_pvs0(pvs0) IMPLIES pvs0_tcc_termination(pvs0)

% ------------------------------------------------------------------------------- %
% PVS0 measure termination implies SCP

  pvs0_tcc_implies_scp : LEMMA
    pvs0_tcc_termination(pvs0) IMPLIES scp_termination_pvs0(pvs0)

% ------------------------------------------------------------------------------- %
% SCP if and only PVS0 termination

  scp_iff_pvs0_terminates : THEOREM
    scp_termination_pvs0(pvs0) IFF terminating?(pvs0)

END scp_iff_pvs0
