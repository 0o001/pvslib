gcd_pvs0: THEORY
BEGIN
% ------------------------------------------------------------------------------ 
% Part0: Definition of gcd in PVS0 
%
  IMPORTING lang[[nat,nat]]

  ebool(m,n:nat) : bool = m /= 0

  boole(b:bool) : Val = (IF NOT b THEN 0 ELSE 1 ENDIF,0)

  eop1(op:nat)(m,n:nat) : Val = 
    COND op = 0 -> boole(m=0 OR n=0),
         op = 1 -> boole(m >= n),
         op = 2 -> (m+n,0),
         op = 3 -> IF m >= n THEN (m-n,n) ELSE (m,n) ENDIF,
	 op = 4 -> (n,m),
         ELSE -> (m,n)
    ENDCOND

  eop2(op:nat)(v1,v2:Val) : Val = v1

  % Interpretation:
  %   vr        : (m,n)
  %   bool(m,n) : n /= 0
  %   op1 0     : (m,n) -> bool(m=0 OR n=0)
  %   op1 1     : (m,n) -> bool(m >= n)
  %   op1 2     : (m,n) -> (m+n,0)
  %   op1 3     : (m,n) -> (m-n,n)
  %   op1 4     : (m,n) -> (n,m)

  pvs0_gcd : PVS0 =
    def(ite(op1(0,vr),op1(2,vr),ite(op1(1,vr),rec(op1(3,vr)),rec(op1(4,vr)))))

% ------------------------------------------------------------------------------ 
% Part1: Proof of termination via pvs0_tccs
%    

  IMPORTING measure_termination_defs
  	    [ [nat,nat]  % Base type
	    , ordinal    % Measure type
	    , ordinals.< % Measure relation
	    ] AS mt_lex2

  IMPORTING pvs0_to_dg[[nat,nat]]

  % This lemma assures that there are one measure type and one measure relation 
  % that can be used to prove termination via pvs0_tcc.
  pvs0_gcd_tcc_termination: LEMMA
    mt_lex2.pvs0_tcc_termination(ebool,eop1,eop2)(pvs0_gcd)

% ------------------------------------------------------------------------------ 
% Part2: Any measure fulfilling pvs0_tcc can be used to prove PVS's tccs.
%
  MT_: TYPE+
  R_ : (well_founded?[MT_])

  IMPORTING measure_termination_defs
  	    [ [nat,nat]  % Base type
	    , MT_        % Measure type
	    , R_         % Measure relation
	    ] AS mt_gen

  wfm_gcd_: mt_gen.WFM

  pvs0_gcd_pvs0_tcc_gen_termination: AXIOM
    mt_gen.pvs0_tcc_termination_(ebool,eop1,eop2)(pvs0_gcd)(wfm_gcd_)

  % By proving the TCCs generated by this importing clause, we are proving that any
  % measure type and any relation such that pvs0_tcc_termination is valid, can be
  % used to prove termination via TCC in PVS.
  IMPORTING gcd_ax {{ MT := MT_, R := R_ , wfm_gcd := wfm_gcd_}}

% ------------------------------------------------------------------------------ 
% Part3: Semantic equivalence between definitions.
%

  IMPORTING measure_termination
  	    [ [nat,nat]  % Base type
  	    , ordinal    % Measure type
  	    , ordinals.< % Measure relation
  	    ] AS termination_concrete

  mf: mt_lex2.WFM = LAMBDA(m,n:nat): lex2(n, m)

  lt_concrete_val(p1,p2: [nat,nat]) : bool = 
    termination_concrete.lt_val(ebool,eop1,eop2)(pvs0_gcd)(mf)(p1,p2)

  IMPORTING orders@omega[[nat,nat], lt_concrete_val]

  Omega_gcd(m,n: nat) : nat = Omega(m,n)

  gcd_pvs0(m,n:nat) : nat =
    val(eval(ebool,eop1,eop2)(Omega_gcd(m,n),pvs0_gcd)(val2env(m,n)))`1
 
  pvs0_gcd_eval : LEMMA
    FORALL(m,n: nat):
      gcd(m,n) = gcd_pvs0(m,n)  

% ------------------------------------------------------------------------------ 
% Part4: Termination by CCG
%

  IMPORTING ccg_to_pvs0[[nat,nat],ebool,eop1,eop2,nat,LAMBDA(n,m:nat):n < m,2]

% ...

END gcd_pvs0
