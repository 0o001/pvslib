%% deriv_examples.pvs
%% 
%% These examples illustrate the use of the strategies derivable, aka
%% differentiable, and derivative.
%%  

deriv_examples : THEORY
BEGIN

  IMPORTING analysis@strategies

  x : VAR real

  f(x:real) : real = x^2+3*x
  g(x:real) : real = -x/2

  %|- deriv_*TCC* : PROOF (then (assert) (derivable)) QED
  %|- derivable_* : PROOF (derivable) QED

  derivable_f : LEMMA
    derivable?(f) 

  derivable_g : LEMMA
    derivable?(g)

  derivable_f_plus_g : LEMMA
    derivable?(f+g) 

  h : [real->nzreal]

  derivable_f_div_h : LEMMA
    derivable?(h) IMPLIES 
    derivable?(f/h)
  %|- derivable_f_div_h : PROOF
  %|- deriv_f_div_h_TCC1
  %|- (then (flatten) (derivable))
  %|- QED

  sq_f(x:real) : real = sq(f(x))

  derivable_sq_f : LEMMA
    derivable?(sq_f)

  derivable_f_o_g : LEMMA
    derivable?(f o g)

  deriv_f : LEMMA
    deriv(f)(x) = 2*x + 3
  %|- deriv_f : PROOF
  %|- (then (skeep) (deriv) (assert))
  %|- QED

  deriv_g : LEMMA
    deriv(g)(x) = -1/2
  %|- deriv_g : PROOF
  %|- (then (skeep) (deriv) (assert))
  %|- QED
 
  deriv_f_plus_g : LEMMA
    deriv(f+g)(x) = 2*x+3-1/2
  %|- deriv_f_plus_g : PROOF
  %|- (then (skeep) (deriv) (assert))
  %|- QED

  %|- deriv_f_div_h_TCC1 : PROOF
  %|- (then (flatten) (derivable))
  %|- QED

  deriv_f_div_h : LEMMA
    derivable?(h) IMPLIES
      deriv(f/h)(x) =
      (3*h(x) + 2*h(x)*x - 3*deriv(h)(x)*x - deriv(h)(x)*x^2)/sq(h(x))
  %|- deriv_f_div_h : PROOF
  %|- (then (skeep) (deriv) (assert))
  %|- QED

  deriv_sq_f : LEMMA
    deriv(sq_f)(x) = 18*x + 18*x^2 + 4*x^3 
  %|- deriv_sq_f : PROOF
  %|- (then (skeep) (deriv) (hide-all-but 1) (grind))
  %|- QED

  deriv_f_o_g : LEMMA
    deriv(f o g)(x) = -3/2 + x/2 
  %|- deriv_f_o_g : PROOF
  %|- (then (skeep) (deriv) (real-props))
  %|- QED

END deriv_examples
