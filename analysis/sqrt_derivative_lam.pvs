sqrt_derivative_lam[T: TYPE FROM real ]: THEORY
%------------------------------------------------------------------------------ 
% This theory has been created to support auto-rewriting, e.g.
%
%    auto-rewrite-theory "derivatives_lam[T]"
%
% or via special strategies
%------------------------------------------------------------------------------ 
BEGIN

  ASSUMING

    IMPORTING deriv_domain_def

    deriv_domain     : ASSUMPTION deriv_domain?[T]

    not_one_element  : ASSUMPTION not_one_element?[T]

  ENDASSUMING

  IMPORTING derivatives_lam[T],
            sqrt_derivative,
	    deriv_domain,
	    chain_rule[T,posreal]

  pos_fun : TYPE = [ T -> posreal ]

  t,x : VAR T
  pp  : VAR pos_fun
  p   : VAR { pp : pos_fun | derivable?(pp) }

  derivable_sqrt_lam       : LEMMA derivable?(LAMBDA(t):sqrt(p(t)))
  derivable_sqrt_lam_point : LEMMA derivable?(pp,x) IMPLIES derivable?(LAMBDA(t):sqrt(pp(t)),x)
  deriv_sqrt_lam           : LEMMA deriv(LAMBDA(t):sqrt(p(t))) = LAMBDA(t): 1/(2*sqrt(p(t))) * deriv(p)(t)

END sqrt_derivative_lam
