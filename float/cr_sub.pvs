%
% Definition of (correctly rounded) substraction. An operation is said to be "cor-
% rectly rounded" when it is computed exactly and then rounded to the nearest
% floating-point number.
%
cr_sub[radix:above(1)]: THEORY
BEGIN

  IMPORTING float[radix],
  	    binop_em_scheme

  b:        VAR Format
  f1,f2:    VAR float
  x1,x2,x3: VAR real

  trivial1(x1):     MACRO bool = TRUE    
  trivial2(x1)(x2): MACRO bool = TRUE    

  Fsub(b)(f1,f2): {f3: (Fcanonic?(b)) | EvenClosest?(b)(FtoR(f1)-FtoR(f2),f3)} =
    Fg[radix,b,trivial1,trivial2,-](f1,f2)

  Fsub_bounded: LEMMA Fbounded?(b)(Fsub(b)(f1,f2))
  AUTO_REWRITE+ Fsub_bounded

  Fsub_error: LEMMA 
    abs(FtoR(Fsub(b)(f1,f2)) - (FtoR(f1)-FtoR(f2))) <= Fulp(b)(Fsub(b)(f1,f2))/2
    
  Fsub_error_ulp: LEMMA 
   FORALL(round: Rounding(b) | FORALL(f:(Fbounded?(b)),x:real):
                                            round(x,f) IMPLIES Closest?(b)(x,f),
          RtoF : RNDF(b,round)):
    abs(FtoR(Fsub(b)(f1,f2)) - (FtoR(f1)-FtoR(f2)))
    <= ulp(b,round,RtoF)(FtoR(f1)-FtoR(f2))/2
    
END cr_sub
