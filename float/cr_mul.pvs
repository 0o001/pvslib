%
% Definition of (correctly rounded) Multiplication.
% An operation is said to be "correctly rounded" when it is computed exactly
% and then rounded to the nearest floating-point number.
%
cr_mul[radix:above(1)]: THEORY
BEGIN

  IMPORTING float[radix],
  	    binop_em_scheme

  b:        VAR Format
  f1,f2:    VAR float
  x1,x2,x3: VAR real

  trivial1(x1):     MACRO bool = TRUE    
  trivial2(x1)(x2): MACRO bool = TRUE    

  Fmul(b)(f1,f2): {f3: (Fcanonic?(b)) | EvenClosest?(b)(FtoR(f1)*FtoR(f2),f3)} =
    Fg[radix,b,trivial1,trivial2,*](f1,f2)

  Fmul_bounded: LEMMA Fbounded?(b)(Fmul(b)(f1,f2))
  AUTO_REWRITE+ Fmul_bounded
  
  Fmul_error: LEMMA 
    abs(FtoR(Fmul(b)(f1,f2)) - (FtoR(f1)*FtoR(f2))) <= Fulp(b)(Fmul(b)(f1,f2))/2
    
  Fmul_error_ulp: LEMMA 
   FORALL(round: Rounding(b) | FORALL(f:(Fbounded?(b)),x:real):
                                            round(x,f) IMPLIES Closest?(b)(x,f),
          RtoF : RNDF(b,round)):
    abs(FtoR(Fmul(b)(f1,f2)) - (FtoR(f1)*FtoR(f2)))
    <= ulp(b,round,RtoF)(FtoR(f1)*FtoR(f2))/2    
    
END cr_mul
