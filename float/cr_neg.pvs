%
% Definition of (correctly rounded) inverse additive operation. 
%
cr_neg[radix:above(1)]: THEORY
BEGIN

  IMPORTING float[radix],
	    unop_em_scheme

  b:        VAR Format
  f1,f2:    VAR float
  x1,x2,x3: VAR real

  neg_pre(x1):MACRO bool = TRUE

  Fneg(b)(f1): {f2: (Fcanonic?(b)) | EvenClosest?(b)(-(FtoR(f1)),f2) } =
    Fg[radix,b,neg_pre,-](f1)
    
  Fneg_bounded: LEMMA
    Fbounded?(b)(Fneg(b)(f1))
  AUTO_REWRITE+ Fneg_bounded

  Fneg_error: LEMMA 
    abs(FtoR(Fneg(b)(f1))-(-(FtoR(f1))))<=Fulp(b)(Fneg(b)(f1))/2
    
  Fneg_error_ulp: CONJECTURE 
   FORALL(round: Rounding(b) | FORALL(f:(Fbounded?(b)),x:real):
                                            round(x,f) IMPLIES Closest?(b)(x,f),
          RtoF : RNDF(b,round)):
    abs(FtoR(Fneg(b)(f1)) - -FtoR(f1)) <= ulp(b,round,RtoF)(-(FtoR(f1)))/2
    
END cr_neg
