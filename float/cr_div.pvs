%
% Definition of (correctly rounded) division. An operation is said to be "cor-
% rectly rounded" when it is computed exactly and then rounded to the nearest
% floating-point number.
%
cr_div[radix:above(1)]: THEORY
BEGIN

  IMPORTING float[radix],
  	    binop_em_scheme

  b:        VAR Format
  f1,f2:    VAR float
  x1,x2,x3: VAR real

  trivial1(x1):     MACRO bool = TRUE    

  div_pre2(x1)(x2):bool = x2/=0

  Fdiv(b)(f1,(f2|div_pre2(FtoR(f1))(FtoR(f2)))):{ f3:(Fcanonic?(b))|
			     EvenClosest?(b)(FtoR(f1)/FtoR(f2),f3) } = 
    Fg[radix,b,trivial1,div_pre2,LAMBDA(x1,(x2|div_pre2(x1)(x2))):x1/x2](f1,f2)

  Fdiv_bounded: LEMMA
   FORALL(f2|div_pre2(FtoR(f1))(FtoR(f2))):
    Fbounded?(b)(Fdiv(b)(f1,f2))
  AUTO_REWRITE+ Fdiv_bounded
  
  Fdiv_error: LEMMA 
   FORALL(f2|div_pre2(FtoR(f1))(FtoR(f2))):
    abs(FtoR(Fdiv(b)(f1,f2)) - (FtoR(f1)/FtoR(f2))) <= Fulp(b)(Fdiv(b)(f1,f2))/2
    
  Fdiv_error_ulp: CONJECTURE
   FORALL(round: Rounding(b) | FORALL(f:(Fbounded?(b)),x:real):
                                            round(x,f) IMPLIES Closest?(b)(x,f),
          RtoF : RNDF(b,round),
	  f1,(f2|div_pre2(FtoR(f1))(FtoR(f2)))):
    abs(FtoR(Fdiv(b)(f1,f2)) - (FtoR(f1)/FtoR(f2)))
    <= ulp(b,round,RtoF)(FtoR(f1)/FtoR(f2))/2    
    
END cr_div
