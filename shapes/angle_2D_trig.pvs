angle_2D_trig : THEORY
  BEGIN 

    IMPORTING trig@sincos,
              angle_2D

    cosine(alpha:Angle_2D) : real_abs_le1 =
      LET (A,C,B) = alpha,
          CA = A-C,
          CB = B-C,
          a = norm(CA),
          b = norm(CB) IN
        (CA*CB)/(a*b) 

    sine(alpha:Angle_2D) : real_abs_le1 =
      LET (A,C,B) = alpha,
          CA = A-C,
          CB = B-C,
          a = norm(CA),
          b = norm(CB) IN
        det(CA,CB)/(a*b) 

    IMPORTING reals@sign3

    sign_cosine_dot : LEMMA
      FORALL (alpha:Angle_2D):
        LET (A,C,B) = alpha,
            CA = A-C,
            CB = B-C IN
        sign3(cosine(alpha)) = sign3(CA*CB)

    sign_sine_det : LEMMA
      FORALL (alpha:Angle_2D):
        LET (A,C,B) = alpha,
            CA = A-C,
            CB = B-C IN
        sign3(sine(alpha)) = sign3(det(CA,CB))
  
    cosine2_sine2_eq_1 : LEMMA
      FORALL (alpha:Angle_2D) :
        sq(cosine(alpha))+sq(sine(alpha)) = 1

    IMPORTING trig@atan2

    % Angle in radians
    angle(alpha:Angle_2D) : nnreal_lt_2pi =
      LET (A,C,B) = alpha,
          CA = A-C,
          CB = B-C,
          a = norm(CA),
          b = norm(CB),
          ab = a*b IN
        atan2((CA*CB)/ab,det(CA,CB)/ab)

    sine_cosine_neq_0 : LEMMA
      FORALL (alpha:Angle_2D):
        cosine(alpha) = 0 IMPLIES sine(alpha) /= 0

    angle_atan2 : LEMMA
      FORALL(alpha:Angle_2D):
        angle(alpha) = atan2(cosine(alpha),sine(alpha))

    cosine_is_cos : LEMMA    
      FORALL (alpha:Angle_2D):
        cosine(alpha) = cos(angle(alpha))

    sine_is_sin : LEMMA    
      FORALL (alpha:Angle_2D):
        sine(alpha) = sin(angle(alpha))
     
    IMPORTING trig@atan2_props
    
    angle_0 : LEMMA
      FORALL (alpha:Angle_2D):
        angle(alpha) = 0 IFF zero?(alpha) 

  END angle_2D_trig
