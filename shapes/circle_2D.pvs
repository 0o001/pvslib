circle_2D : THEORY
  BEGIN 

    IMPORTING circular_segment_2D

    circle_2D?(CS:CircularSegment_2D) : bool =
      LET (A,C,B) = arc(CS) IN
        A = B

    Circle_2D : TYPE = (circle_2D?)    
  
    mk_circle(C:Vect2,R:posreal) : Circle_2D =
      LET A = C+(R,0) IN
        mk_circular_segment(A,C,A)

    % Function returns true if point Q is inside circle CC (including borders) 
    point_in_circle?(CC:Circle_2D)(Q:Vect2) : bool =
      LET (A,C,B) = arc(CC) IN
        sqv(Q-C) <= sqv(A-C)

    point_in_circle_eq : LEMMA
      FORALL (CC:Circle_2D,clockwise:bool) :
        point_in?(CC,clockwise) = point_in_circle?(CC)

    % Function returns true if point Q is inside circle CC (excluding borders) 
    point_strictly_in_circle?(CC:Circle_2D)(Q:Vect2) : bool =
      LET (A,C,B) = arc(CC) IN
        sqv(Q-C) < sqv(A-C)

    point_strictly_in_circle_eq : LEMMA
      FORALL (CC:Circle_2D,clockwise:bool) :
        point_strictly_in?(CC,clockwise) = point_strictly_in_circle?(CC)

  END circle_2D