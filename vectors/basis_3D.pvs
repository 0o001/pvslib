basis_3D: THEORY
%------------------------------------------------------------------------------
%     Orthonormal Basis for 3D Vectors    
%------------------------------------------------------------------------------

BEGIN

   IMPORTING vectors_3D

   e1,e2,e3  : VAR Vect3
   w,w1,w2: VAR Vect3
   a1,a2,a3,b1,b2,b3  : VAR real

   orthogonal?(e1,e2,e3): bool =
     orthogonal?(e1,e2) AND
     orthogonal?(e2,e3) AND
     orthogonal?(e3,e1)

  orthogonal_kernel: LEMMA
     e1 /= zero AND
     e2 /= zero AND
     e3 /= zero AND
     orthogonal?(e1,e2,e3) AND
     a1*e1+a2*e2+a3*e3=zero
     IMPLIES
     (a1=0 AND a2=0 AND a3=0)

   orthogonal_spans: LEMMA
          e1 /= zero AND
     e2 /= zero AND
     e3 /= zero AND
     orthogonal?(e1,e2,e3) IMPLIES
     EXISTS (a1,a2,a3):
       w = a1*e1+a2*e2+a3*e3

   orthogonal_dot_zero: LEMMA
     e1 /= zero AND
     e2 /= zero AND
     e3 /= zero AND
     orthogonal?(e1,e2,e3) AND
     w*e1=0 AND w*e2=0 AND w*e3=0 IMPLIES
     w=zero

   orthogonal_basis: LEMMA
     e1 /= zero AND
     e2 /= zero AND
     e3 /= zero AND
     orthogonal?(e1,e2,e3)
     IMPLIES
     w = ((w*e1)/sqv(e1))*e1 + ((w*e2)/sqv(e2))*e2 + ((w*e3)/sqv(e3))*e3

   orthonormal?(e1,e2,e3): bool =  
     orthogonal?(e1,e2,e3) AND e1 /= zero AND norm(e1) = 1 AND
     e2 /= zero AND norm(e2) = 1 AND e3 /= zero AND norm(e3) = 1

   orthonormal_basis: LEMMA FORALL (w:Vect3):
     orthonormal?(e1,e2,e3) IMPLIES
     w = (w*e1)*e1 + (w*e2)*e2 + (w*e3)*e3

   basis_add: LEMMA
     %% orthonormal?(e1,e2) AND
     w1 = (w1*e1)*e1 + (w1*e2)*e2 + (w1*e3)*e3 AND
     w2 = (w2*e1)*e1 + (w2*e2)*e2 + (w2*e3)*e3
   IMPLIES 
     w1+w2 = ((w1+w2)*e1)*e1 + ((w1+w2)*e2)*e2 + ((w1+w2)*e3)*e3

   basis_sub: LEMMA
     %% orthonormal?(e1,e2) AND
     w1 = (w1*e1)*e1 + (w1*e2)*e2 + (w1*e3)*e3 AND
     w2 = (w2*e1)*e1 + (w2*e2)*e2 + (w2*e3)*e3
   IMPLIES 
     w1-w2 = ((w1-w2)*e1)*e1 + ((w1-w2)*e2)*e2 + ((w1-w2)*e3)*e3

   basis_sqv: LEMMA FORALL (a,b,c: real,w: Vect3):
     orthonormal?(e1,e2,e3) AND
     w = (a)*e1 + (b)*e2 + (c)*e3
     IMPLIES
        sqv(w) = sq(a) + sq(b) + sq(c)

   basis_dot: LEMMA  FORALL (a,b,c,d,e,f: real):
     orthonormal?(e1,e2,e3) IMPLIES
     LET s = (a)*e1 + (b)*e2 + (c)*e3,
         v = (d)*e1 + (e)*e2 + (f)*e3
     IN
        s*v = a*d + b*e + c*f

  orthogonal_basis_sqv: LEMMA
    orthogonal?(e1,e2,e3) AND
    w = a1*e1 + a2*e2 + a3*e3 IMPLIES
    sqv(w) = sq(a1)*sqv(e1) + sq(a2)*sqv(e2) + sq(a3)*sqv(e3)

  orthonormal_basis_sqv: LEMMA
    orthonormal?(e1,e2,e3) AND
    w = a1*e1 + a2*e2 + a3*e3 IMPLIES
    sqv(w) = sq(a1) + sq(a2) + sq(a3)

  orthogonal_basis_norm: LEMMA
    orthogonal?(e1,e2,e3) AND
    w = a1*e1 + a2*e2 + a3*e3 IMPLIES
    norm(w) = sqrt(sq(a1)*sqv(e1) + sq(a2)*sqv(e2) + sq(a3)*sqv(e3))

  orthonormal_basis_norm: LEMMA
    orthonormal?(e1,e2,e3) AND
    w = a1*e1 + a2*e2 + a3*e3 IMPLIES
    norm(w) = sqrt(sq(a1) + sq(a2) + sq(a3))

  orthogonal_basis_dot: LEMMA
    orthogonal?(e1,e2,e3) AND
    w1 = a1*e1 + a2*e2 + a3*e3 AND
    w2 = b1*e1 + b2*e2 + b3*e3 IMPLIES
    w1*w2 = (a1*b1)*sqv(e1) + (a2*b2)*sqv(e2)
    	    + (a3*b3)*sqv(e3)

  orthonormal_basis_dot: LEMMA
    orthonormal?(e1,e2,e3) AND
    w1 = a1*e1 + a2*e2 + a3*e3 AND
    w2 = b1*e1 + b2*e2 + b3*e3 IMPLIES
    w1*w2 = a1*b1 + a2*b2 + a3*b3


END basis_3D

