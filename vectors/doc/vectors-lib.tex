\documentclass[12pt]{article}
\usepackage{fullpage}
\usepackage{url}
\usepackage{amssymb}
\usepackage{amsmath}
\usepackage{graphics}

\usepackage{macros}

\usepackage[dvips,usenames]{color}
%\newcommand{\ntx}{\color{red}}
\newcommand{\ntx}{}


\fulltrue % REMARK. Use \fulltrue to compile a complete file
           % (including unfinished texts inside \iffull ... \fi sections)



\begin{document}

\title{NASA Langley PVS Vectors Library}


\newpage
\section{Vectors Library}

The NASA PVS library contains three distinct vectors libraries
\begin{enumerate}
\item 2-dimensional vectors
\item 3-dimensional vectors
\item N-dimensional vectors
\end{enumerate}
One might wonder why there should be 2D and 3D versions, when an N-dimensional
version is available.  The answer is that there are some notational
conveniences for doing this.  For example, in the 2D version we represent
a vector as 
\begin{verbatim}
   Vector: TYPE = [#  x, y: real  #]
\end{verbatim}
whereas in the N-dimensional library a vector is
\begin{verbatim}
   Index     : TYPE = below(n) 
   Vector    : TYPE = [Index -> real]
\end{verbatim}
where \pvs{n} is a formal parameter (\pvs{posnat}) to the theory.
Thus, in the two dimensional case, the x-component of a vector \pvs{v} is
\pvs{v`x} whereas in the N-dimensional library it is \pvs{v(0)}.  Also
certain operations are greatly simplified in the 2D case.  The dot
product is
\begin{verbatim}
   *(u,v): real  =  u`x * v`x + u`y * v`y;          % dot product
\end{verbatim}
in the 2-dimensional case, whereas in the N-dimensional case it is
\begin{verbatim}
   *(u,v): real = sigma(0,n-1,LAMBDA i:u(i)*v(i));     % Dot Product
\end{verbatim}
where sigma is a summation operator imported from the reals library.

In this appendix we will present the 2-dimensional version because
that is what is used in the SATS work.  However, the differences in
the libraries are kept to a minimum.  All operators, definitions,
and lemmas are given identical names to simplify the use of these
libraries.

\subsection{2D Vectors}

Two names are available for a vector type are provided in the theory
\pvs{vectors2D}.
\begin{verbatim}
   Vector    : TYPE = [#  x, y: real  #]
   Vect2     : TYPE = Vector
\end{verbatim}
The vector operators are defined as follows:
\begin{verbatim}
   a     : VAR real
   u,v,w : VAR Vector

   -(v)  : Vector = (-v`x, -v`y); 

   +(u,v): Vector = (u`x + v`x, u`y + v`y);

   -(u,v): Vector = (u`x - v`x, u`y - v`y);

   *(u,v): real  =  u`x * v`x + u`y * v`y;          % dot product

   *(a,v): Vector = (a * v`x, a * v`y);
\end{verbatim}
A conversion is provided so that one can create 2D vectors as follows
\begin{verbatim}
        (xv,yv)
\end{verbatim}
rather than having to write
\begin{verbatim}
        (# x := xv, y := yv #)
\end{verbatim}
There are several functions and predicates provided such as
\begin{verbatim}
   sqv(v): nnreal = v*v
   norm(v): nnreal = sqrt(sqv(v))

   zero_vector?(v) : MACRO bool = (norm(v) = 0  AND 
                                   v`x = 0 AND v`y = 0)     

   nz_vector?(v)   : MACRO bool = (norm(v) /= 0 AND 
                                   (v`x /= 0 OR v`y /= 0))  

   normalized?(v)  : MACRO bool = (norm(v) = 1)
 
   zero    : Zero_vector =  (0,0) ;

   ^(nzv)         : Normalized = (1/norm(nzv))*nzv          

   parallel?(nzu,nzv): bool = ^(nzu)*^(nzv) = 1 OR  
                              ^(nzu)*^(nzv) = -1

   orthogonal?(u,v): bool =  u * v = 0 ;
\end{verbatim}
There are several dozen lemmas available for manipulating vectors
such as
\begin{verbatim} 
   add_assoc           : LEMMA u+(v+w) = (u+v)+w
   add_move_right      : LEMMA u + w = v IFF u = v - w
   add_cancel_left     : LEMMA u + v = u + w IMPLIES v = w   
   neg_distr_sub       : LEMMA -(v - u) = u - v
   dot_eq_args_ge      : LEMMA u*u >= 0
   dot_distr_add_right : LEMMA (v+w)*u = v*u + w*u
   dot_scal_left       : LEMMA (a*u)*v = a*(u*v)
   dot_scal_canon      : LEMMA (a*u)*(b*v) = (a*b)*(u*v)   
   sqv_scal            : LEMMA sqv(a*v) = sq(a)*sqv(v)
   sqrt_sqv_norm       : LEMMA sqrt(sqv(v)) = norm(v)
   norm_eq_0           : LEMMA norm(v) = 0 IFF v = zero
   cauchy_schwartz     : LEMMA sq(u*v) <= sqv(u)*sqv(v)
\end{verbatim}

\subsection{Positions in 2D space}

The theory \pvs{positions2D} enhances the vector space with constructs
for specifying distances.  One frequently wants to use a vector to
designate a location in 2D space.  To make this more explicit, the
following type definition was added
\begin{verbatim}
   Pos2D: TYPE = Vect2
\end{verbatim}
though it is really just a synonym.  Next it is useful to have
a metric or distance function:
\begin{verbatim}
   sq_dist(p1,p2: Pos2D): nnreal = sq(p1`x - p2`x) + sq(p1`y - p2`y)

   dist(p1,p2: Pos2D)   : nnreal = sqrt(sq_dist(p1,p2))
\end{verbatim}
Many lemmas are available, including
\begin{verbatim}
   dist_refl    : LEMMA dist(p,p) = 0
   dist_sym     : LEMMA dist(p1,p2) = dist(p2,p1)
   dist_eq_0    : LEMMA dist(p1,p2) = 0 IFF p1 = p2 
   dist_norm    : LEMMA dist(u,v) = norm(u-v)
   sq_dist_le   : LEMMA sq_dist(v1,v2) <= sq_dist(p1,p2) IMPLIES
                            dist(v1,v2) <= dist(p1,p2)     
   dist_ge_x    : LEMMA dist(p1,p2) >= abs(p1`x - p2`x)  
   dist_ge_y    : LEMMA dist(p1,p2) >= abs(p1`y - p2`y) 
   dist_triangle: LEMMA sq(dist(p2,p0)) = sq(dist(p1,p0)) + sq(dist(p1,p2)) 
                                             - 2*(p1-p0)*(p1-p2) 
\end{verbatim}
The following predicates are available:
\begin{verbatim}
   on_circle?(p,r): bool = dist(p,zero) = r   

   on_line?(p1,p2,p): bool = 
                EXISTS (x : real) : p = p1 + x * (p2 - p1) 

   on_segment?(p1,p2,p): bool = 
                EXISTS (x : { y: nnreal | y <= 1}) : p = p1 + x * (p2 - p1) 
\end{verbatim}

\subsection{2D Lines}

The theory \pvs{lines2D} provides convenient formalizations for lines
in 2-dimensional space.  The traditional way to defines a line $L$ is
by specifying two distinct points, $\vec{p_0}$ and $\vec{p_1}$, on
it. A line $L$ can also be defined by a point and a direction.  Let
$\vec{p_0}$ be a point on the line $L$ and let $\vec{dv}$ be a nonzero
vector specifying the direction of the line.  This is equivalent to
the two point definition, since we could just put $\vec{dv} =
(\vec{p_1}-\vec{p_0})$.  We can also add dynamics to our line.  If we
assume a particle is moving in a line with a constant velocity, then
we can define this linear motion using the location of the point at
time zero, a velocity vector and a time parameter $t$:
\begin{eqnarray*}
      \vec{p_0} + t*\vec{\pvs{vel}}
\end{eqnarray*}
which provides the location of the particle at time $t$.

In the library, lines are defined as a tuple:
\begin{verbatim}
                                     %     Basic        |   Dynamic
                                     %------------------|-----------------
   Line : TYPE = [# p: Vect2,        % point on the line| position at time 0
                    v: Nz_vect2 #]   % direction vector | velocity vector
  
   Line2D: TYPE = Line   
\end{verbatim}
This enables one to represent a line using a point and a direction
vector
\begin{verbatim}
     p(L) + v(L)                or             L`p + L`v
\end{verbatim}
or using a point and a velocity vector
\begin{verbatim}
     p(L) + t v(L)              or             L`p + t * L`v
\end{verbatim}
The following alternate field names are provided
\begin{verbatim}
   p0 (L: Line): MACRO Vect2 = p(L)  % alternate field names
   vel(L: Line): MACRO Vect2 = v(L)
\end{verbatim}
For example
\begin{verbatim}
    L`p0 + t * L`vel
\end{verbatim}
This can be appreviated using the following macro:
\begin{verbatim}
   loc(L: Line)(tt: real): MACRO Vect2 = p(L) + tt*v(L)
\end{verbatim}
Two functions are provided to calculate the velocity vector
for different situations:
\begin{center}
\begin{tabular}{lp{5.0in}}
\pvs{vel\_from\_tm}:  & generates velocity vector from two points and transport time \\ 
\pvs{vel\_from\_spd}: & generates velocity vector from two points and speed \\ 
\end{tabular}
\end{center}
These are defined as follows
\begin{verbatim}
  vel_from_tm(p1,p2,t): { v | p2 = p1 + t*v } = 1/t*(p2 - p1)

  vel_from_spd(p1,p2,s):  Vect2 = IF p1 = p2 then zero
                                  ELSE s/dist(p1,p2)*(p2-p1)
                                  ENDIF
\end{verbatim}
{\ntx Other} useful lemmas include
\begin{verbatim}
  vel_from_tm_rew     : LEMMA vel_from_tm(p1,p2,t) = 1/t*(p2 - p1)
  vel_from_tm_eq_args : LEMMA vel_from_tm(p,p,t) = zero
  vel_from_spd_lem    : LEMMA p1 /= p2 IMPLIES
             vel_from_spd(p1,p2,ps) = vel_from_tm(p1,p2,dist(p1,p2)/ps)
  vel_from_spd_norm   : LEMMA p1 /= p2 IMPLIES
             vel_from_spd(p1,p2,s) = s*normalize(p2-p1)
\end{verbatim}
Some predicates on lines are also provided:
\begin{verbatim}
  L,L1,L2: VAR Line

  on_line?(p,L): bool = EXISTS (x : real) : p = p(L) + x * v(L)

  on_segment?(p,L): bool =
               EXISTS (x : { y: nnreal | y <= 1}) : p = p(L) + x * v(L)

  orthogonal?(L1,L2): bool = ^(v(L1))*^(v(L2)) = 0 

  parallel?(L1,L2)  : bool = ^(v(L1))*^(v(L2)) = 1 OR ^(v(L1))*^(v(L2)) = -1 
\end{verbatim}

\subsection{Intersecting Lines}

The theory \pvs{intersections2D} provides some efficient
methods for determining whether two lines intersect or not and the 
point of intersection if they do so.  The theory is built around
a function named \pvs{cross}: 
\[
   \pvs{cross}(p,q) = p_x*q_y - q_x*p_y
\]
The following simple property hold for \pvs{cross}:  
\[
   \pvs{cross}(p,q) = -\pvs{cross}(q,p)
\]

There are three cases for two lines $L0$ and $L1$:
\begin{quote}
\begin{tabular}{lp{4.4in}}
      intersecting: &   $\pvs{cross}(L0_v,L1_v) \neq 0$ \\
      parallel:     &   $\pvs{cross}(L0_v,L1_v) =  0$ ~AND~
                        $\pvs{cross}(\Delta,L0_v) \neq 0$ \\
      same line:    &   $\pvs{cross}(L0_v,L1_v) =  0$ ~AND~
                        $\pvs{cross}(\Delta,L0_v) = 0$ \\
\end{tabular}
\end{quote}
where $\Delta = L1_p - L0_p$.
Correspondingly, the library provides the following predicates:
\begin{verbatim}
   intersect?(L0,L1): bool = cross(L0`v,L1`v) /= 0 

   same_line?(L0,L1): bool = LET DELTA = L1`p - L0`p IN  
                        cross(L0`v,L1`v) = 0 AND cross(DELTA,L0`v) = 0
\end{verbatim}
Given two lines that intersect the function \pvs{intersect\_pt} returns
the intersection point:
\begin{verbatim}
   intersect_pt(L0:Line2D,L1: Line2D | cross(L0`v,L1`v) /= 0): Pos2D =
                               LET DELTA = L1`p - L0`p,
                                  ss = cross(DELTA,L1`v)/cross(L0`v,L1`v) IN 
                                  L0`p + ss*L0`v
\end{verbatim}
Several key lemmas are provided:
\begin{verbatim}
   intersection_lem     : LEMMA cross(L0`v,L1`v) /= 0 IMPLIES
                                LET DELTA = L1`p - L0`p,
                                   ss = cross(DELTA,L1`v)/cross(L0`v,L1`v),
                                   tt = cross(DELTA,L0`v)/cross(L0`v,L1`v)
                                IN
                             L0`p + ss*L0`v =  L1`p + tt*L1`v

   pt_intersect         : LEMMA on_line?(p,L0) AND on_line?(p,L1) AND
                                NOT same_line?(L0,L1) IMPLIES
                                    intersect?(L0,L1) 

   intersect_pt_unique  : LEMMA intersect?(L0,L1) IMPLIES
                                pnot /= intersect_pt(L0,L1) AND
                                on_line?(pnot,L0)  
                             IMPLIES
                                NOT on_line?(pnot,L1)

   same_line_lem        : LEMMA p0 /= p1 AND
                               ( on_line?(p0,L0) AND on_line?(p0,L1) AND
                                 on_line?(p1,L0) AND on_line?(p1,L1) )
                                    IMPLIES same_line?(L0,L1)
        
   not_same_line        : LEMMA on_line?(p,L0) AND 
                                NOT on_line?(p,L1) 
                             IMPLIES
                                 NOT same_line?(L0,L1)
                         
    intersect_pt_lem     : LEMMA NOT same_line?(L0,L1) AND
                                on_line?(pnot,L0)  AND
                                on_line?(pnot,L1)  
                             IMPLIES
                                 intersect_pt(L0,L1) = pnot 
\end{verbatim}

\subsection{Closest Approach}

The theory \pvs{closest\_approach\_2D} provides some tools to
calculate the point of closest approach (CPA) between two points that
are dynamically moving in a straight line.  This is an important
computation for collision detection. For example, this can be used to
calculate the time and distance of two aircaft (represented as line
vectors) when they are at their closest point.


Suppose we have two time-parametric linear equations 
\[
   \vec{p}(t) = \vec{p_0} + t \vec{u} \;\;\;    \;\;\;    \;\;\;   
   \vec{q}(t) = \vec{q_0} + t \vec{v}     
\]
Minimum separation occurs at:
\[
    t_{\pvs{cpa}} = -\frac{\vec{w_0}(\vec{u}-\vec{v})}{|\vec{u}-\vec{v}|^2}
\]
where $\vec{w_0} = \vec{p_0} - \vec{q_0}$.
The library provides a function \pvs{time\_closest}: 
\begin{verbatim}
  time_closest(p0,q0,u,v): real = 
     IF norm(u-v) = 0 THEN  % parallel, eq speed
        0
     ELSE
        -((p0-q0)*(u-v))/sq(norm(u-v))
     ENDIF 
\end{verbatim}
The following lemma gives an alternate way to calculate the function.
\begin{verbatim}
  time_closest_lem: LEMMA norm(u-v) /= 0 AND
                          a = (u-v)*(u-v) AND
                          b = 2*(p0-q0)*(u-v) 
                          IMPLIES
                            time_closest(p0,q0,u,v) = -b/(2*a)
\end{verbatim}
The lemma \pvs{time\_cpa} establishes that this time is indeed the
point where the distance is at a minimum. 
\begin{verbatim}
  time_cpa: LEMMA t_cpa = time_closest(p0,q0,u,v) 
               IMPLIES
                  is_minimum?(t_cpa,(LAMBDA t: sq_dist(p0+t*u,q0+t*v))) 
\end{verbatim}
See
\begin{verbatim}
   http://geometryalgorithms.com/Archive/algorithm_0106/algorithm_0106.htm
\end{verbatim}
for a very nice discussion.



\end{document}
 

