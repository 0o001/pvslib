cd2d_shape[D:posreal] : THEORY
BEGIN

  IMPORTING cd2d,
            horizontal,
	    vectors@vect2_more,
            shapes@shapes_2D

  cd2d_rectangle : LEMMA
    FORALL(so,si:Vect2,v:Nz_vect2,T:posreal) :
      LET s   = so-si,
          vD = hmd_tangent_point[D](v),
          A   = so+vD,
          B   = so-vD,
          vT  = T*v IN    
      point_strictly_in_rectangle?(mk_rectangle(A,B,-norm(vT)))(si)
      IFF
       abs(s*perpL(^(v))) < D AND
       0 < -s*v < T *sqv(v)

  cd2d_hazard_zone(so,v:Vect2,T:nnreal) : shapes_2D =
    IF T=0 OR zero_vector?(v) THEN
      (: Circle(mk_circle(so,D)) :)
    ELSE
      LET vD = hmd_tangent_point[D](v),
          A   = so+vD,
          B   = so-vD,
          vT  = T*v,
          soT = so+vT,
          C   = soT-vD,
          D   = soT+vD IN    
      (: CircularSegment(mk_circular_segment(A,so,B)), 
         Rectangle(mk_rectangle(A,B,-norm(vT))),
         CircularSegment(mk_circular_segment(C,soT,D)) :)
    ENDIF

  cd2d_haz_correctness : LEMMA
    FORALL(so,vo,si,vi:Vect2,T:nnreal,ss:shapes_2D) :
      LET ss = cd2d_hazard_zone(so,vo-vi,T) IN
      point_strictly_in?(ss)(si) IFF
      IF T=0 THEN horizontal_los?[D](so-si)
      ELSE cd2d?[D,0,T](so-si,vo-vi)
      ENDIF

END cd2d_shape
