% -------------------------------------------------------------------
%  File Name: Sig_int.pvs
%  Author Name: Gilberto Pérez
%  	  	Felicidad Aguado 
%  	  	Pablo Ascariz
%		Pedro Cabalar
%		Concepción Vidal
% -------------------------------------------------------------------
%   This theory defines the valuation of formulas and provides
%   related concepts and properties
% -------------------------------------------------------------------
%  This file is a single component of a library of logic theories. 
%  ------------------------------------------------------------------
%  For the most part, we follow the treatmet in the following text:
%
% F. Aguado, P. Cabalar, D. Pearce, G. Pérez, C. Vidal,
% "A denotational semantics for equilibrium logic",
% in Proc. of the  31st Intl. Conf. on Logic Programming, 2015.
%
%--------------------------------------------------------------------


Sig_int[T: TYPE+]: THEORY

 BEGIN


 IMPORTING Sig_prop[T], Sig_form[T]



% Interpretations of formulas
 I_form: TYPE = [Sig_form -> S3]

 IC_form: TYPE = [Sig_form -> S2]

 I_IC_form: JUDGEMENT IC_form SUBTYPE_OF I_form 



% Valuations of formulas
 v_form(s: Sig_form,i: I): RECURSIVE S3 =
 	   CASES s OF
	   bot: 0,
	   atom(t): i(t),
	   op_and(t1,t2): min(v_form(t1,i),v_form(t2,i)),
	   op_or(t1,t2): max(v_form(t1,i),v_form(t2,i)),
	   op_imp(t1,t2): IF v_form(t1,i) <= v_form(t2,i) THEN 2 ELSE v_form(t2,i) ENDIF
	   ENDCASES
	   MEASURE s BY <<



% OP_AND lemma
 caractv_form_and2: LEMMA FORALL (t1,t2: Sig_form,i: I): v_form(op_and(t1,t2),i) = 2 IFF 
        v_form(t1,i)=2 AND v_form(t2,i)=2


% OP_OR lemma
 caractv_form_or2: LEMMA FORALL (t1,t2: Sig_form,i: I): v_form(op_or(t1,t2),i) = 2 IFF 
        v_form(t1,i)=2 OR v_form(t2,i)=2


% OP_IMP lemmas
 caractv_form_imp2: LEMMA FORALL (t1,t2: Sig_form,i: I): v_form(op_imp(t1,t2),i) = 2 IFF 
        v_form(t1,i)=0 OR v_form(t2,i)=2 OR (v_form(t1,i)= 1 AND v_form(t2,i)=1)

 caractv_form_imp0: LEMMA FORALL (t1,t2: Sig_form,i: I): v_form(op_imp(t1,t2),i) = 0 IFF 
        not(v_form(t1,i)=0) AND v_form(t2,i)=0
     
 caractv_form_imp1: Lemma FORALL (t1,t2: Sig_form,i: I): v_form(op_imp(t1,t2),i) = 1 IFF 
        v_form(t1,i)=2 AND v_form(t2,i)=1



 v_form_total: LEMMA FORALL (t: Sig_form,i: I): v_form(t,RT(i))=0 OR v_form(t,RT(i))=2



% Truth table style lemmas
 caract_val_zero: LEMMA FORALL (t: Sig_form,i: I): v_form(t,i) = 0 IFF v_form(t,RT(i))=0

 val_two: LEMMA FORALL (t: Sig_form,i: I): v_form(t,i) = 2 IMPLIES v_form(t,RT(i))=2

 val_one: LEMMA FORALL (t: Sig_form,i: I): v_form(t,i) >= 1 IMPLIES v_form(t,RT(i))=2
     
 caract_val_nozero: LEMMA FORALL (t: Sig_form,i: I): v_form(t,i) >= 1 IFF v_form(t,RT(i))=2


 END Sig_int
