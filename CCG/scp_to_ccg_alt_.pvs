scp_to_ccg_alt_[Val,Expr,Condition: TYPE,
               (IMPORTING cc_def[Val,Expr,Condition]) CCs:finite_set[CallingContext],
               semantic_rel:PRED[[Expr,Val,Val]],eval_conds:PRED[[Condition,Val]],
    % Importings needed to work with uninterpreted digraphs
    ConcreteDigraph: TYPE,
    (IMPORTING cc_def[Val,Expr,Condition]) 
    vert:  [ConcreteDigraph-> [CallingContext -> bool]],
    edges: [ConcreteDigraph -> [CallingContext,CallingContext -> bool]],
    make:  [ v:(is_finite[CallingContext]),
             {e: (is_finite[[CallingContext,CallingContext]]) | 
	         FORALL(x,y:CallingContext): 
    	          e(x,y) IMPLIES v(x) AND v(y) } -> ConcreteDigraph] ]: THEORY 
BEGIN
  ASSUMING
    % This theory only makes sense if the parametric type ConcreteDigraph is a 
    % correct interpretation for (uninterpreted) digraphs. That is checked using 
    % the predicate defined in digraphs_abs.
    IMPORTING digraphs@digraphs_abs[CallingContext,ConcreteDigraph,vert,edges,make]
    concretedigraph_implements_abstractdigraph: ASSUMPTION
      digraphs_abs.correct_implementation

    IMPORTING scp[Val,Expr,Condition,[nat->(CCs)]]

    scp_termination : ASSUMPTION scp_termination?(semantic_rel,eval_conds)

    semantic_rel_finite_branch: ASSUMPTION
      FORALL(expr: Expr, v: Val):
 	is_finite({ w: Val | semantic_rel(expr,v,w) })
  ENDASSUMING

  IMPORTING digraphs@digraphs_[CallingContext, ConcreteDigraph,vert,edges,make]

  Seq_cc: TYPE = [nat->(CCs)]

  R (x,y: Val): bool = 
    EXISTS(cc: (CCs)):
      eval_conds(cc`conds,y) AND
      semantic_rel(cc`actuals,y,x) 

  IMPORTING orders@well_foundedness[Val]

  R_wf: LEMMA
    well_founded?(R)

  IMPORTING finite_sets@finite_sets_eq, finite_sets_of_sets[Val]

  R_no_desc_inf_branch: LEMMA
    FORALL(x: Val):
      is_finite({y: Val | R(y, x)})

  IMPORTING orders@omega[Val,R],
            ccg_[Val,Expr,Condition,nat,LAMBDA(n,m:nat):n<m,1,semantic_rel,eval_conds,
		 ConcreteDigraph,vert,edges,make]

  Graph_cc : TYPE = {dg:ConcreteDigraph | subset?(vert(dg),CCs)}

  scp_implies_ccg_termination : THEOREM
    FORALL (dg:Graph_cc):
      EXISTS (fms:FunMeasures) : ccg_termination?(make_ccg(dg,fms))

END scp_to_ccg_alt_
