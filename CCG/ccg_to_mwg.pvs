%********************************************************************************%
%                                                                                %
%   Authors:                                                                     %
%                                                                                %
%       Andréia Borges Avelar -- Universidade de Brasília - Brasil               %
%       Mauricio Ayala-Rincon -- Universidade de Brasília - Brasil               %
%                 Cesar Muñoz -- NASA Langley Research Center - US               %
%                                                                                %
%********************************************************************************%
%                    last modified: Mar - 03 - 2014                              %
%********************************************************************************%

ccg_to_mwg[Val,Expr,Condition: TYPE, Vr: TYPE FROM Expr,
           MT: TYPE, lt : (well_founded?[MT]), N: posnat,
           semantic_rel:PRED[[Expr,[Vr->Val],Val]],eval_conds:PRED[[Condition,[Vr->Val]]]]: THEORY
 BEGIN

% ------------------------------------------------------------------------------- %

 IMPORTING ccg[Val,Expr,Condition,Vr,MT,lt,N,semantic_rel,eval_conds],
	   matrix_wdg[CallingContext,N]


   G: VAR CCG
   w: VAR prewalk
   e: VAR edgetype
   v: VAR CallingContext
 i,j: VAR below(N)


 indi_func(G)(i, j: below(N), c: vert(dg(G))) : Sign3 =
   IF    gt(ms(G),c,i,j,FALSE) THEN 1
   ELSIF gt(ms(G),c,i,j,TRUE) THEN 0
   ELSE -1
   ENDIF

 weight(G)(e: edge(dg(G))): [below(N) -> [below(N) -> Sign3]] =
   LAMBDA(i: below(N))(j: below(N)): indi_func(G)(i, j, e`1)

 ccg_to_mwg(G: CCG) : wdg = 
    (# dg := dg(G),
      wgt := weight(G) #)

% ------------------------------------------------------------------------------- %
% Some basic properties between a G: CCG and a G1:= ccg_to_mwg(G: CCG) 
% ------------------------------------------------------------------------------- %

 same_edges: LEMMA
   LET G1 = ccg_to_mwg(G) IN edge?(dg(G))(e) IFF edge?(dg(G1))(e)

 same_verts: LEMMA
   LET G1 = ccg_to_mwg(G) IN vert?(dg(G))(v) IFF vert?(dg(G1))(v)

 same_walks: LEMMA
   LET G1 = ccg_to_mwg(G) IN walk?(dg(G), w) IFF walk?(dg(G1), w)

% ------------------------------------------------------------------------------- %
% Lemmas associating the operators gte_mc? and defined_pos?
% ------------------------------------------------------------------------------- %

 gte_mc_to_defined_pos: LEMMA
   FORALL(G: CCG, w: Walk(dg(G))): FORALL(mc: measures_combination(w)):
     LET G1 = ccg_to_mwg(G) IN
       gte_mc?(ms(G), w)(mc) IMPLIES
       defined_pos?(wgt_walk(G1, w), mc(0), mc(length(w) - 1))

 defined_pos_to_gte_mc: LEMMA
   FORALL(G: CCG, w: Walk(dg(G))): LET G1 = ccg_to_mwg(G) IN
     FORALL(i, j: below(N)): 
       defined_pos?(wgt_walk(G1, w), i, j) IMPLIES 
       EXISTS(mc: measures_combination(w)): gte_mc?(ms(G), w)(mc)
       AND first(mc) = i AND last(mc) = j

 no_defined_pos_iff_no_gte_mc: LEMMA
   FORALL(G: CCG, w: Walk(dg(G))): LET G1 = ccg_to_mwg(G) IN 
      (FORALL(i, j: below(N)): NOT defined_pos?(wgt_walk(G1, w), i, j))
     IFF
      (FORALL(mc: measures_combination(w)): NOT gte_mc?(ms(G), w)(mc))

% ------------------------------------------------------------------------------- %
% Lemmas associating the operators gt_mc? and positive_pos?
% ------------------------------------------------------------------------------- %

 gt_mc_to_positive_pos: LEMMA
   FORALL(G: CCG, w: Walk(dg(G))): FORALL(mc: measures_combination(w)):
     LET G1 = ccg_to_mwg(G) IN
       gt_mc?(ms(G), w)(mc) IMPLIES
       positive_pos?(wgt_walk(G1, w), mc(0), mc(length(w) - 1))

 positive_pos_to_gt_mc: LEMMA
   FORALL(G: CCG, w: Walk(dg(G))): LET G1 = ccg_to_mwg(G) IN
     FORALL(i, j: below(N)):
       positive_pos?(wgt_walk(G1, w), i, j) IMPLIES
       EXISTS(mc: measures_combination(w)): gt_mc?(ms(G), w)(mc)
       AND first(mc) = i AND last(mc) = j

 no_positive_pos_no_gt_mc: LEMMA
   FORALL(G: CCG, w: Walk(dg(G))): LET G1 = ccg_to_mwg(G) IN 
      (FORALL(i, j: below(N)): NOT positive_pos?(wgt_walk(G1, w), i, j))
     IFF
      (FORALL(mc: measures_combination(w)): NOT gt_mc?(ms(G), w)(mc))


% ------------------------------------------------------------------------------- %
%    Lemma associating positive measure-matrixes 
%    and greater than measure-combinations
% ------------------------------------------------------------------------------- %

 positive_iff_gt_mc: LEMMA
   FORALL(G: CCG, w: Walk(dg(G))): LET G1 = ccg_to_mwg(G) IN
      positive?(wgt_walk(G1, w))
    IFF
      (EXISTS(mc: measures_combination(w) | first(mc) = last(mc)):
       gt_mc?(ms(G), w)(mc))

 mwg_termination_iff_ccg_termination: LEMMA
   LET G1 = ccg_to_mwg(G) IN
     mwg_termination?(G1) IFF ccg_termination?(G)

 END ccg_to_mwg
