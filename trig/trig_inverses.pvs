trig_inverses: THEORY
%----------------------------------------------------------------------------
% Interface to Inverse Trig Functions (Connecting Multiple Definitions)
%
% NOTE: We begin we smaller domains where the derivatives are defined
%
%  sin, cos <- tan <- atan (inverse) <- asin, acos
%        \
%         \-- arc_sin (inverse), arc_cos(inverse)  <- arcsin, arccos  == asin, acos
%                  (-1,1)                              [-1,1]
%
% Rick Butler 6/23/17
%----------------------------------------------------------------------------
BEGIN
   IMPORTING trig_basic, asin, acos, atan, 
             arcsin, arccos, analysis@derivative_props[real_abs_lt1]

  a,b,c,d           : VAR real

  arcsin_eq_asin: LEMMA arcsin = asin

  arccos_eq_acos: LEMMA arccos = acos

  arccos_arcsin: LEMMA FORALL (y: real_abs_le1): 
     arccos(y) = pi/2 - arcsin(y)

%  -------------------- Arctan --------------------

   arctan_prep: LEMMA FORALL (x: real_abs_lt_pi2): Tan?(x);

   arctan(y: real): {x: real_abs_lt_pi2 | y = tan(x)} = atan(y)

   tan_arctan : LEMMA (FORALL (y: real): tan(arctan(y)) = y)
   arctan_tan : LEMMA (FORALL (x: real_abs_lt_pi2): arctan(tan(x)) = x)


% Use derivatives equal ==> functions equals,  must deal with slightly different domains

  sin_asin: LEMMA FORALL (x: real_abs_le1): sin(asin(x)) = x
  cos_acos: LEMMA FORALL (x: real_abs_le1): cos(acos(x)) = x
  tan_atan: LEMMA tan(atan(a)) = a

  asin_sin: LEMMA FORALL (x:real_abs_le_pi2): asin(sin(x)) = x
  acos_cos: LEMMA FORALL (x:nnreal_le_pi):   acos(cos(x)) = x
  atan_tan: LEMMA FORALL (x:real_abs_lt_pi2): atan(tan(x)) = x


  AUTO_REWRITE+ asin_0
  AUTO_REWRITE+ asin_1
  AUTO_REWRITE+ acos_0
  AUTO_REWRITE+ acos_1
  AUTO_REWRITE+ atan_0

  %% The following were moved from trig_basic

  arc_sin_cos  : LEMMA sq(a)+sq(b)=sq(c) IMPLIES
                            EXISTS d: a = c*cos(d) AND b = c*sin(d)

  nnc          : VAR nonneg_real 
  pythagorean  : LEMMA sq(a)+sq(b)=sq(nnc) IMPLIES
                            EXISTS(alpha:real): nnc=a*cos(alpha)+b*sin(alpha)


END trig_inverses
