sincos: THEORY    
BEGIN

  IMPORTING reals@quadratic, sincos_def, pi_def, trig_types
  IMPORTING analysis@deriv_domain, analysis@deriv_domains
  IMPORTING analysis@nth_derivatives[real]
  IMPORTING series@power_series
  IMPORTING merten
  IMPORTING reals@polynomials

  AUTO_REWRITE- abs_0
  AUTO_REWRITE- abs_nat

  a,b,c,x,x0,alpha:  VAR real
  e,px:  VAR posreal
  n:     VAR nat

  noa_real_lt_pi2       : LEMMA not_one_element?[real_abs_lt_pi2]
  noa_posreal_lt_pi     : LEMMA not_one_element?[posreal_lt_pi]

  deriv_domain_real_abs_lt_pi2  : LEMMA deriv_domain?[real_abs_lt_pi2]
  deriv_domain_posreal_lt_pi    : LEMMA deriv_domain?[posreal_lt_pi]

  AUTO_REWRITE+ noa_real_lt_pi2  
  AUTO_REWRITE+ noa_posreal_lt_pi    
  AUTO_REWRITE+ deriv_domain_real_abs_lt_pi2
  AUTO_REWRITE+ deriv_domain_posreal_lt_pi  

  sin_range   : JUDGEMENT sin(a) HAS_TYPE real_abs_le1
  cos_range   : JUDGEMENT cos(a) HAS_TYPE real_abs_le1

  cos_ub: LEMMA cos(x)        <= 1
  sin_ub: LEMMA sin(px)       <  px
  cos_lb: LEMMA 1-px*px/2     <  cos(px)
  sin_lb: LEMMA px-px*px*px/6 <  sin(px)


  cos_pos_bnds:    LEMMA 1-px*px/2     < cos(px) AND cos(px) <= 1
  sin_pos_bnds:    LEMMA px-px*px*px/6 < sin(px) AND sin(px) <  px


%%  sin_convergence  : LEMMA convergence(NQ(sin,x),0,cos(x))
  sin_derivable    : LEMMA derivable?(sin,x)
  sin_derivable_fun: LEMMA derivable?(sin)
  deriv_sin_fun    : LEMMA deriv(sin) = cos

  IMPORTING analysis@chain_rule[real,real]

  sin_linear_deriv: LEMMA derivable?(LAMBDA x: c * sin(a * x + b)) AND
     deriv(LAMBDA x: c * sin(a * x + b)) = (LAMBDA x: a * c * cos(a * x + b)) 

%%  cos_convergence  : LEMMA convergence(NQ(cos,x),0,-sin(x))
  cos_derivable    : LEMMA derivable?(cos,x)
  cos_derivable_fun: LEMMA derivable?(cos)
  deriv_cos_fun    : LEMMA deriv(cos) = -sin

  cos_linear_deriv: LEMMA derivable?(LAMBDA x: c * cos(a * x + b)) AND
     deriv(LAMBDA x: c * cos(a * x + b)) = (LAMBDA x: -a * c * sin(a * x + b)) 

  sin_continuous   : LEMMA continuous?(sin,x0)
  cos_continuous   : LEMMA continuous?(cos,x0)
  sin_cont_fun     : LEMMA continuous?(sin)
  cos_cont_fun     : LEMMA continuous?(cos)

 % ----------------- Sum and Difference of Two Angles ----------------

  cos_plus  : LEMMA cos(a + b) = cos(a)*cos(b) - sin(a)*sin(b)
  cos_minus : LEMMA cos(a - b) = cos(a)*cos(b) + sin(a)*sin(b)
  sin_plus  : LEMMA sin(a + b) = sin(a)*cos(b) + cos(a)*sin(b)
  sin_minus : LEMMA sin(a - b) = sin(a)*cos(b) - sin(b)*cos(a)

 % -------------------- Co-Function Identities -----------------------

  cos_pi2     : LEMMA cos(pi/2) = 0
  sin_pi2     : LEMMA sin(pi/2) = 1

  cos_sin    : LEMMA cos(a) = sin(a+pi/2)
  sin_cos    : LEMMA sin(a) = -cos(a+pi/2)
  sin_shift  : LEMMA sin(pi/2 - a) = cos(a)
  cos_shift  : LEMMA cos(pi/2 - a) = sin(a)

% --------------------- Arguments involving pi ---------------------

  neg_cos     : LEMMA -cos(a) = cos(a+pi)
  neg_sin     : LEMMA -sin(a) = sin(a+pi)

  cos_pi      : LEMMA cos(pi) = -1
  sin_pi      : LEMMA sin(pi) = 0
  sin_3pi2    : LEMMA sin(3*pi/2) = -1
  cos_3pi2    : LEMMA cos(3*pi/2) = 0
  sin_2pi     : LEMMA sin(2*pi) = 0
  cos_2pi     : LEMMA cos(2*pi) = 1


% ------------------------- Periodicity -----------------------------

  j,k: VAR integer

  sin_period  : LEMMA sin(a) = sin(a + 2 * j * pi)
  cos_period  : LEMMA cos(a) = cos(a + 2 * j * pi)

  sin_k_pi    : LEMMA sin(k*pi) = 0
  cos_2k_pi   : LEMMA cos(2*k*pi) = 1
  cos_k_pi    : LEMMA cos(k*pi) = (-1)^(k)
  sin_k_pi2   : LEMMA sin(k*pi + pi/2) = (-1)^k 

% -------------------- Characterization of zeroes -------------------

  sin_pos_0tohalfpi: LEMMA FORALL (a:{x:real | 0 < x AND x <= pi/2}): sin(a) > 0

  sin_pos_0topi: LEMMA FORALL (a:{x:real | 0 < x AND x < pi}): sin(a) > 0

  sin_neg_pito2pi: LEMMA FORALL (a:{x:real | pi < x AND x < 2 * pi}): sin(a) < 0
		       
  sin_eq_0_2pi : LEMMA 0 <= a AND a <= 2*pi IMPLIES
                    (sin(a)=0 IFF a=0 OR a=pi OR a=2*pi)

  cos_eq_0_2pi : LEMMA 0 <= a AND a <= 2*pi IMPLIES
                    (cos(a)=0 IFF a=pi/2 OR a=3*pi/2)


  sin_eq_0_prep: LEMMA FORALL (x:real): EXISTS (j:int): j-1 <= x AND x <= j
  sin_eq_0     : LEMMA sin(a) = 0 IFF EXISTS (i: int): a = i * pi

  cos_eq_0     : LEMMA cos(a) = 0 IFF EXISTS (i: int): a = i * pi + pi/2


  nderiv_sin_fun:  LEMMA derivable_n_times?(sin,n) AND
                         nderiv(n,sin) = LAMBDA (x:real): sin((n*pi/2)+x)
  nderiv_cos_fun:  LEMMA derivable_n_times?(cos,n) AND
                         nderiv(n,cos) = LAMBDA (x:real): cos((n*pi/2)+x)

  sin_series_term(x:real):[nat->real]
    = (LAMBDA (n:nat): (-1)^n*x^(2*n+1)/factorial(2*n+1))
  sin_series_n(x:real,n:nat):real
    = sigma(0,n,LAMBDA (i:nat): IF i>n THEN 0 ELSE sin_series_term(x)(i) ENDIF)
  cos_series_term(x:real):[nat->real]
    = (LAMBDA (n:nat): IF n=0 THEN 1 ELSE (-1)^n*x^(2*n)/factorial(2*n) ENDIF)
  cos_series_n(x:real,n:nat):real
    = sigma(0,n,LAMBDA (i:nat): IF i>n THEN 0 ELSE cos_series_term(x)(i) ENDIF)

  sin_taylors: LEMMA (EXISTS (c: between(0,x)): 
    sin(x) = sin_series_n(x,n)+nderiv(2*n+3,sin)(c)*x^(2*n+3)/factorial(2*n+3))

  cos_taylors: LEMMA (EXISTS (c: between(0,x)): 
    cos(x) = cos_series_n(x,n)+nderiv(2*n+2,cos)(c)*x^(2*n+2)/factorial(2*n+2))

  sin_series: LEMMA abs(sin(x)-sin_series_n(x,n))
                      <= abs(x^(2*n+3))/factorial(2*n+3)
  cos_series: LEMMA abs(cos(x)-cos_series_n(x,n))
                      <= abs(x^(2*n+2))/factorial(2*n+2)
		      
END sincos
