acos: THEORY

  BEGIN

  IMPORTING asin

  x:       VAR real_abs_le1
  xne:     VAR real_abs_lt1
  nnx,nny: VAR {x:nnreal | x <= 1}

  acos(x:real_abs_le1):nnreal_le_pi = pi/2 - asin(x)
  
  acos_neg:               LEMMA acos(-x) = pi-acos(x)
  acos_0:                 LEMMA acos(0)  = pi/2
  acos_sqrt_half:         LEMMA acos(sqrt(1/2)) = pi/4
  acos_1:                 LEMMA acos(1)  = 0
  acos_minus1:            LEMMA acos(-1) = pi
  acos_minus_sqrt_half:   LEMMA acos(-sqrt(1/2)) = 3*pi/4
  acos_strict_decreasing: LEMMA strict_decreasing?(acos)
  acos_bij:               LEMMA bijective?[real_abs_le1,nnreal_le_pi](acos)

  acos_derivable:  LEMMA
                derivable?[real_abs_lt1]((LAMBDA (x:real_abs_lt1):acos(x)),xne)

  acos_derivable_fun: LEMMA
                derivable?[real_abs_lt1](LAMBDA (x:real_abs_lt1): acos(x))

  deriv_acos_fun:  LEMMA deriv[real_abs_lt1](LAMBDA (x:real_abs_lt1): acos(x))
     = (LAMBDA (x:real_abs_lt1): -deriv[real_abs_lt1](LAMBDA (x: real_abs_lt1): asin(x))(x))


  deriv_acos_prep: LEMMA FORALL (x: real_abs_lt1): 1 - sq(x) > 0

  deriv_acos:  LEMMA deriv[real_abs_lt1](LAMBDA (x:real_abs_lt1): acos(x))
                         = (LAMBDA (x:real_abs_lt1): -1/sqrt(1-sq(x)))

  cos_atan: LEMMA FORALL (x: real): cos(atan(x)) = 1/sqrt(1+sq(x))
  
END acos
