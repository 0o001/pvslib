arcsin: THEORY
%----------------------------------------------------------------------------
% Interface to Inverse Trig Functions (Connecting Multiple Definitions)
%
% NOTE: We begin we smaller domains where the derivatives are defined
%
%  sin, cos <- tan <- atan (inverse) <- asin, acos
%        \
%         \-- arc_sin (inverse), arc_cos(inverse)  <- arcsin, arccos  == asin, acos
%                  (-1,1)                              [-1,1]
%
% Rick Butler 6/23/17
%----------------------------------------------------------------------------
BEGIN
   IMPORTING trig_basic, trig_ineq,
             derivative_props[real_abs_lt_pi2]

  a: VAR real

  sin_restrict_range: LEMMA
     FORALL (x: real_abs_lt_pi2): -1 < sin(x) AND sin(x) < 1;

%  -------------------- sin_restrict --------------------

  sin_restrict(x: real_abs_lt_pi2): MACRO real_abs_lt1 = sin(x)

  sin_restrict_inj  : LEMMA injective?(sin_restrict)
  sin_restrict_surj : LEMMA surjective?[real_abs_lt_pi2, real_abs_lt1](sin_restrict)
  sin_restrict_bij  : LEMMA bijective?(sin_restrict)

  del_neigh_lem: LEMMA del_neigh_all?[real_abs_lt_pi2]

  deriv_sin_restrict: LEMMA derivable?[real_abs_lt_pi2](sin_restrict) AND
                             deriv[real_abs_lt_pi2](sin_restrict) 
                                  = (LAMBDA (x: real_abs_lt_pi2): cos(x))

%  -------------------- Arc_Sin --------------------

  arc_sin_exists: LEMMA
      EXISTS (f: [y: real_abs_lt1 -> {x: real_abs_lt_pi2 | y = sin(x)}]): TRUE


  arc_sin(y: real_abs_lt1): {x: real_abs_lt_pi2 | y = sin(x)}

  arc_sin_inverse: LEMMA arc_sin = inverse(sin_restrict)

  sin_arc_sin: LEMMA (FORALL (y: real_abs_lt1): sin(arc_sin(y)) = y)
  arc_sin_sin: LEMMA (FORALL (x: real_abs_lt_pi2): arc_sin(sin(x)) = x)

  arc_sin_0:  LEMMA arc_sin(0) = 0

  IMPORTING analysis@derivative_inverse

  arc_sin_derivable: LEMMA derivable?[real_abs_lt1](arc_sin) 


  arc_sin_range_lt: LEMMA FORALL (x: real_abs_lt1): 
      -pi / 2 < arc_sin(x) AND arc_sin(x) < pi / 2

  deriv_arc_sin_prep: LEMMA FORALL (x: real_abs_lt1): 1 - sq(x) > 0

  deriv_arc_sin_fun: THEOREM derivable?[real_abs_lt1](arc_sin) AND
                          deriv[real_abs_lt1](arc_sin) = (LAMBDA (x:real_abs_lt1): 1/sqrt(1-sq(x)))    
  arc_sin_continuous: LEMMA continuous?[real_abs_lt1](arc_sin)

  arc_sin_bij: LEMMA bijective?[real_abs_lt1,real_abs_lt_pi2](arc_sin)

  arcsin(y: real_abs_le1): real_abs_le_pi2 =
      IF y = -1 THEN -pi/2 ELSIF y < 1 THEN arc_sin(y) ELSE pi/2 ENDIF

END arcsin
