ECEF: THEORY
%----------------------------------------------------------------------------
%
% Earth-Centered, Earth-Fixed Cartesian coordinate system
%
%----------------------------------------------------------------------------
BEGIN

  IMPORTING trig@trig_basic,
  	    vectors@vectors_3D,
	    trig@acos,
	    trig@atan2,
	    trig@trig_ineq,
	    trig@trig_inverses

  r : VAR posreal % Radius of the Earth
  lat,lon : VAR real

  spherical2xyz(r,lat,lon): Nz_vect3 =   % Convert LatLon to ECEF
    LET theta = pi/2 - lat,
    	phi   = pi-lon,
  	x     = r*sin(theta)*cos(phi),
  	y     = r*sin(theta)*sin(phi),
  	z     = r*cos(theta)
    IN (x,y,z)

  spherical2xyz_norm: LEMMA
    LET vv = spherical2xyz(r,lat,lon) IN norm(vv) = r

  sin_acos_ecef: LEMMA FORALL (sig:{s:real|abs(s)<=1}):
    sin(acos(sig)) = sqrt(1 - sq(sig))

  xyz2spherical(x,y,z:real): [# r:{rr:nnreal|(x/=0 OR y/=0 OR z/=0) IMPLIES rr>0},
  			   lat:real,lon:real #] =
    LET r = sqrt(x^2+y^2+z^2),
    	rxy = sqrt(x^2+y^2)
    IN
      IF rxy = 0 AND z>=0 THEN (# r:=r,lat:=pi/2,lon:=0 #)
      ELSIF rxy = 0 THEN (# r:=r,lat:=-pi/2,lon:=0 #) 
      ELSE
        LET theta = acos(z/r),
	    phi = atan2(x,y),
	    lat = pi/2-theta,
	    lon = pi-phi
        IN (# r:=r,lat:=lat,lon:=lon #)
      ENDIF

  spherical2xyz_def: LEMMA FORALL (x,y,z:real):
    (x/=0 OR y/=0 OR z/=0) IMPLIES
    LET A = xyz2spherical(x,y,z),
    	r = A`r,lat = A`lat, lon = A`lon
    IN spherical2xyz(r,lat,lon) = (x,y,z)

  xyz2spherical_def: LEMMA 
    (-pi/2<lat AND lat<pi/2) AND
    (-pi<lon AND lon<pi) IMPLIES
    LET v = spherical2xyz(r,lat,lon) IN
      xyz2spherical(v`x,v`y,v`z)=
      (# r:=r, lat:=lat, lon:=lon #)

END ECEF
