sin_props : THEORY

  BEGIN
  
  IMPORTING fast_approx, trig_fnd@trig_approx
  
  sin_iter_alt_series: LEMMA FORALL ((x:nnreal |x<7), eps:posreal,
      n:nat, (b:real | b= IF n=0 THEN 0 ELSE sin_approx(x,n-1) ENDIF), 
      (c:nnreal | c=x^(2*n+1)/factorial(2*n+1) AND c>eps AND c*x^2/((2*n+3)*(2*n+2))<=eps)):
   sin_approx_iter_alt(x,eps,n,b,c) = sin_approx(x,n) 

   sin_iter_alt_series2: LEMMA FORALL ((x:nnreal |x<7), eps:posreal,
      n:nat, m:posnat, (b:real | b=IF n=0 THEN 0 ELSE sin_approx(x,n-1) ENDIF), 
      (c:nnreal | c=x^(2*n+1) AND (FORALL (mm:below(m)): c*x^(2*mm)/factorial(2*(n+mm)+1)>eps) AND c*x^(2*m)/factorial(2*(n+m)+1)<=eps)):
   sin_approx_iter_alt(x,eps,n,b,c/factorial(2*n+1)) = sin_approx(x,(n+m-1))

  sin_iter_alt_series3: LEMMA FORALL ((x:nnreal |x<7), eps:posreal, 
     (n: posnat | (FORALL (m:below(n)): x^(2*m+1)/factorial(2*m+1)>eps) AND x^(2*n+1)/factorial(2*n+1)<=eps)):
    sin_approx_iter_alt(x, eps, 0, 0, x) = sin_approx(x,n-1)

  bounding?((x:posreal |x<7), eps:posreal, n:nat): bool = x^(2*n+1)/factorial(2*n+1)<=eps
 
  bound_iter((x:posreal | x<7), eps:posreal, (n:nat| FORALL (m:below(n+1)): x^(2*m+1)/factorial(2*m+1)>eps)) : 
     RECURSIVE {nn:posnat| (FORALL (m:below(nn)): x^(2*m+1)/factorial(2*m+1)>eps) AND x^(2*nn+1)/factorial(2*nn+1)<=eps } = 
      IF bounding?(x,eps, n+1) THEN n+1 
      ELSE bound_iter(x, eps, n+1)
      ENDIF
      MEASURE lex2(IF x^(2*n+3)/factorial(2*n+3)<=eps THEN 0 
	       	       	    ELSIF n<7 THEN 8-n
			    ELSE 0 ENDIF, 
			    IF x^(2*n+3)/factorial(2*n+3)<=eps THEN 0 
			    ELSE 1+log_nat((x^(2*n+3)/factorial(2*n+3))/eps, 4)`1
		       	    ENDIF)

  iter_bound: LEMMA FORALL ((x:nnreal | x<7), eps:posreal):
    x<=eps OR (EXISTS (nn:posnat): (FORALL (m:below(nn)): x^(2*m+1)/factorial(2*m+1)>eps) AND x^(2*nn+1)/factorial(2*nn+1)<=eps )

 sin_app_bound: LEMMA FORALL ((x:real | abs(x)<7), eps:posreal):
      abs(sin(x)-sin_app(x,eps)) <= eps

  END sin_props
