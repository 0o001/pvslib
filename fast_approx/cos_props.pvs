cos_props : THEORY

  BEGIN

  IMPORTING fast_approx, trig_fnd@trig_approx
  
  cos_iter_alt_series: LEMMA FORALL ((x:posreal |x<7), eps:posreal,
      n:nat, (b:real | b= IF n=0 THEN 0 ELSE cos_approx(x,n-1) ENDIF), 
      (c:nnreal | c=x^(2*n)/factorial(2*n) AND c>eps AND c*x^2/((2*n+1)*(2*n+2))<=eps)):
   LET N = IF b+pm1(n)*c/=0 THEN n ELSE n+1 ENDIF IN
   cos_approx_iter_alt(x,eps,n,b,c) = cos_approx(x,N) 

   cos_iter_alt_series2: LEMMA FORALL ((x:posreal |x<7), eps:posreal,
      n:nat, m:posnat, (b:real | b=IF n=0 THEN 0 ELSE cos_approx(x,n-1) ENDIF), 
      (c:nnreal | c=x^(2*n) AND (FORALL (mm:below(m)): c*x^(2*mm)/factorial(2*(n+mm))>eps) AND c*x^(2*m)/factorial(2*(n+m))<=eps)):
    LET N = IF b+sigma(n,n+m-1, cos_term(x))/=0 THEN n+m-1 ELSE n+m ENDIF IN
   cos_approx_iter_alt(x,eps,n,b,c/factorial(2*n)) = cos_approx(x, N)

  cos_iter_alt_series3: LEMMA FORALL ((x:posreal |x<7), eps:posreal, 
     (n: posnat | (FORALL (m:below(n)): x^(2*m)/factorial(2*m)>eps) AND x^(2*n)/factorial(2*n)<=eps)):
      LET N= IF cos_approx(x,n-1)/=0 THEN n-1 ELSE n ENDIF IN
    cos_approx_iter_alt(x, eps, 0, 0, 1) = cos_approx(x,N)

  bounding?((x:posreal |x<7), eps:posreal, n:nat): bool = x^(2*n)/factorial(2*n)<=eps
 
  bound_iter((x:posreal | x<7), eps:posreal, (n:nat| FORALL (m:below(n+1)): x^(2*m)/factorial(2*m)>eps)) : 
     RECURSIVE {nn:posnat| (FORALL (m:below(nn)): x^(2*m)/factorial(2*m)>eps) AND x^(2*nn)/factorial(2*nn)<=eps } = 
      IF bounding?(x,eps, n+1) THEN n+1 
      ELSE bound_iter(x, eps, n+1)
      ENDIF
      MEASURE lex2(IF x^(2*n+2)/factorial(2*n+2)<=eps THEN 0 
	       	       	    ELSIF n<7 THEN 8-n
			    ELSE 0 ENDIF, 
			    IF x^(2*n+2)/factorial(2*n+2)<=eps THEN 0 
			    ELSE 1+log_nat((x^(2*n+2)/factorial(2*n+2))/eps, 4)`1
		       	    ENDIF)

  iter_bound: LEMMA FORALL ((x:posreal | x<7), eps:posreal):
    1<=eps OR (EXISTS (nn:posnat): (FORALL (m:below(nn)): x^(2*m)/factorial(2*m)>eps) AND x^(2*nn)/factorial(2*nn)<=eps )

 cos_app_bound: LEMMA FORALL ((x:real | abs(x)<7), (eps:posreal | eps<1)):
      abs(cos(x)-cos_app(x,eps)) <= eps

  END cos_props
