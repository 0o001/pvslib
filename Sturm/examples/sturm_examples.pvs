sturm_examples: THEORY
BEGIN

  IMPORTING Sturm@strategies

  x,y : VAR real

%|- example_* : PROOF
%|- (sturm)
%|- QED

  example_1: LEMMA 
    160*((x-1/2)^2*(x-9)^4*(x+8.5)^2 + 0.1) /= 0 

  example_2: LEMMA 
     (x-1/2)^2*(x-9)^4*(x+8.5)^2 >= 0

  example_3: LEMMA 
   (x^16-x^15-3*x^13+x^12-x^11+2*x^10-6*x^9+8*x^8 
   -7*x^7-6*x^6+5*x^5+4*x^4-3*x^3+350*x + 478)^2 /= 0

  example_4: LEMMA 
    x<0 IMPLIES x^101 < -x

  example_5: LEMMA 
    x<0 IMPLIES x^5-2*x^3 < 6

  example_6: LEMMA
    x^2 - 2*x + 1 >= 0

  example_7: LEMMA
    x*(1-x) <= 1/4

  example_8: LEMMA
    x<0 IMPLIES x^5 /= 0

  example_9: LEMMA
    x^120-2*x^60+1 >= 0

  example_sos : LEMMA
    ((x-1)^8 + 2*(x-x)^8 + (x-3)^8 -2)/4 >= 0

%% Examples with explicit quantifications

  example_10: LEMMA
    EXISTS (x:real) : x >= 0 AND x^2 - x < 0

  example_11: LEMMA
    EXISTS (x:real) : x >= 0 AND x^2 - x = 0

  example_12: LEMMA 
    FORALL (x:real) : x<0 IMPLIES x^5 < -x

  example_13 : LEMMA
    FORALL (x:real | abs(x) <= 1) : (x-1)*(x+1) <= 0

  example_14 : LEMMA
    FORALL (x:real): x>0 IMPLIES x^15*(2-x)^20*(6-x)^2*(12-x)^4*(20-x)^6>=0

%% Examples with interval notations, including extended intervals

  example_15 : LEMMA 
    x ## [|0,3|] IMPLIES x^120 -2*x^60 + 1 >= 0

  example_16 : LEMMA 
    EXISTS (x:real): x ## [|0,3|] AND x^120 -2*x^60 + 1 = 0

  example_17 : LEMMA
    x ## [|-oo,open(3)|] IMPLIES
    x^120 - (2/3)*x^60 + 1/9 >= 0

  example_18 : LEMMA
    EXISTS (x:real) : x ## [|-oo,open(3)|] AND 
                      x^120 - (2/3)*x^60 + 1/9 >= 0

  example_19 : LEMMA 
    FORALL (x:real | x ## [|-2,1|]) : x^4-x^3-19*x^2-11*x+30 <= 31.7

  example_20 : LEMMA 
    FORALL (x:real) : x ## [| 0, oo |] IMPLIES -x^3 <= 0

  example_21 : LEMMA 
    FORALL (x:real) : x ## [| open(1), oo |] IMPLIES -x^3 < 1

  example_22 : LEMMA 
    FORALL (x:real) : x ## [| -oo, 1 |] IMPLIES x*(1-x) <= 0.25

% Examples with negated quantifications

%|- n_example_* : PROOF
%|- (sturm -1)
%|- QED

  n_example_1: LEMMA
    NOT (FORALL (x:real) : x >= 0 IMPLIES x^2 >= x)

  n_example_2: LEMMA
    NOT (FORALL (x:real) : x >= 0 IMPLIES x^2 /= x)

  n_example_3: LEMMA
    NOT (EXISTS (x:real): x < 0 AND x^5 >= -x)

  n_example_4: LEMMA
    NOT (EXISTS (x:real): x < 0 AND x^5 = 0)

  n_example_5 : LEMMA 
    NOT EXISTS (x:posreal) : x^3 <= 0

%% Examples of monotonicity

%|- mono_example_* : PROOF
%|- (mono-poly)
%|- QED

  mono_example_1 : LEMMA
    FORALL (x,y:real) : x >= 1 AND x < y IMPLIES (x-1/4)^2 <= y*y-(1/2)*y+(1/16)

  mono_example_2 : LEMMA
    FORALL (x,y:real) : x < y IMPLIES x^5 /= y^5

  mono_example_3: LEMMA
    FORALL (x,y:real) : 0.4 <= x AND x < y AND y <= 0.6 IMPLIES
    x^5-x^3+x/5>y*(y^4-y^2+1/5)

  mono_example_4: LEMMA
    FORALL (x,y:real): x /= y IMPLIES x^3 /= y^3

  mono_example_5: LEMMA
    EXISTS (x,y:real): x < y AND x^2 >= sq(y)

  mono_example_6: LEMMA
    EXISTS (x:real,y:real|y>=-1): x > y AND x^4 < y^4

%% Other examples

 legendre : LEMMA
    abs(x) < 1 IMPLIES
    3969/65536 + 63063/4096 * x^6 + 1792791/4096 * x^10 +
    3002285/4096 * x^18 + 6600165/4096 * x^14
    - 72765/65536 * x^4 - 3558555/32768 * x^8
    - 10207769/65536 * x^20 - 35043645/32768 * x^12
    - 95851899/65536 * x^16 > 0
%|- legendre : PROOF
%|- (sturm)
%|- QED

  legendre3 : LEMMA
    abs(x) < 1 IMPLIES
    (3969/65536 + 63063/4096 * x^6 + 1792791/4096 * x^10 +
     3002285/4096 * x^18 + 6600165/4096 * x^14
     - 72765/65536 * x^4 - 3558555/32768 * x^8
     - 10207769/65536 * x^20 - 35043645/32768 * x^12
     - 95851899/65536 * x^16)^3 > 0
%|- legendre3 : PROOF
%|- (sturm)
%|- QED

  legendre_8(x:real) : MACRO real =
    (6435*x^8 - 12012*x^6 + 6930*x^4 - 1260*x^2 + 35)/128

  legendre_9(x:real) : MACRO real =
    (12155*x^9 - 25740*x^7 + 18018*x^5 - 4620*x^3 + 315*x)/128

  legendre_10(x:real) : MACRO real = 
    (46189*x^10 -109395*x^8 + 90090*x^6 - 30030*x^4 + 3465*x^2 - 63)/256

  Turan_9: LEMMA
    abs(x) < 1 IMPLIES
    legendre_9(x)^2 > legendre_8(x)*legendre_10(x)
%|- Turan_9 : PROOF
%|- (sturm)
%|- QED

  Legendre_10 : LEMMA
    x ## [|-0.75,-0.6|] AND y ## [|-0.75,-0.6|] AND x < y IMPLIES
    legendre_10(x) > legendre_10(y)
%|- Legendre_10 : PROOF
%|- (mono-poly)
%|- QED

%|- *_TCC* : PROOF
%|- (subtype-tcc)
%|- QED


END sturm_examples
