best_rational: THEORY

% Continued fractions give the
% best rational in a real interval
%


BEGIN

  x,y : VAR posreal
  i   : VAR int
  n   : VAR nat
  pn  : VAR posnat
  r1,r2,r3,r4: VAR real

  exp_lower_bd: LEMMA FORALL (epsil:{r:posreal|r>=1},k:nat): epsil^k >= 1+k*(epsil-1)

  min_pow_ge(epsil:{r:posreal|r>1},D:posreal,p:nat): {k:nat|
    k>=p AND epsil^k>=D AND (FORALL (d:nat): (d>=p AND epsil^d>=D) IMPLIES d>=k)}

  frac_nn_meas(epsil:posreal): nat = IF epsil>=1 THEN 0 ELSE min_pow_ge(1/(1-epsil),1/epsil,0) ENDIF

  frac_nn_meas_decreasing: LEMMA x<y AND y-x<1 AND y<=1
    IMPLIES (frac_nn_meas(y-x)=0 AND frac_nn_meas(1/x-1/y)=0) OR
            (frac_nn_meas(y-x)/=0 AND frac_nn_meas(1/x-1/y)<frac_nn_meas(y-x))

  frac_nn_int(x,(y|x<y)): RECURSIVE {r:posrat | x<=r AND r<=y} =
    LET Fx = floor(x), Fy = floor(y) IN
    IF Fx/=Fy THEN Fy
    ELSIF Fx=x THEN Fx
    ELSIF x>1 THEN Fx + 1/frac_nn_int(1/(y-Fy),1/(x-Fx))
    ELSE 1/frac_nn_int(1/y,1/x)
    ENDIF MEASURE frac_nn_meas(y-x)

  floors_neq_diff_1: LEMMA
    x<=y AND y-x>=1 IMPLIES floor(x)/=floor(y)

  frac_nn_int_increasing: LEMMA
    FORALL (x,y,z,w:posreal): 
      x<y AND z<w AND x<=z AND y<=w
      IMPLIES frac_nn_int(x,y)<=frac_nn_int(z,w)

  best_rational(r1,(r2|r1<r2)): {q:rat | r1<=q AND q<=r2} =
    IF (r1<=0 AND r2>=0) OR r1=0 OR r2=0 THEN 0
    ELSIF r1>0 THEN frac_nn_int(r1,r2)
    ELSE -frac_nn_int(-r2,-r1) ENDIF

  best_rational_increasing: LEMMA
    r1<=r3 AND r2<=r4 AND r1<r2 AND r3<r4 IMPLIES
    best_rational(r1,r2)<=best_rational(r3,r4)

END best_rational
