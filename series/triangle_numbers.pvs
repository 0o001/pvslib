triangle_numbers  % Welcome
		: THEORY

%Created by         J Tanner Slagel
%	 	           Feb 2021
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| This is a formalization of the
%| summation of the reciprocal of
%| the triangle numbers
%| sum_i=0^infy 1/T(i+1) = 2,
%| where T(i) is the sum of the
%| first i natural numbers.
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Last updated	           Feb 2021

%-----     %
  BEGIN
%     -----%

%----------------------------------------
%% Importing the theory on infinite series
%  which also contains sigma, the finite
%  summation function in PVS
%----------------------------------------

IMPORTING  series

%----------------------------------------
%% Define the nth triangle number as the
%  sum of the first n natural numbers
%----------------------------------------

Tri(n:nat): nat =
 sigma(0, n, LAMBDA(i:nat): i)
 
%----------------------------------------
%% Recursive relationship between
%  triangle numbers
%----------------------------------------

Tri_rec: LEMMA
 FORALL(n:nat):
 Tri(n) + n+1 = Tri(n+1)

%----------------------------------------
%% Gauss' identity that say the sum
%  of the first n triangle numbers is
%  n*(n+1)/2
%----------------------------------------

Gauss_idx: LEMMA
 FORALL(n:nat):
  Tri(n) = n * (n + 1)/2

%----------------------------------------
%% Define reciprocal of triangle numbers.
%  Shifting to remove 0th triangle number
%----------------------------------------

rTri(n:nat):
 posreal = 1 / Tri(n + 1)

%----------------------------------------
%% Finite sum of reciprocal of triangle
%  numbers has a nice form from
%  telescoping
%----------------------------------------

sum_rTri_telescope: LEMMA
 FORALL(n:nat):
  series(rTri)(n)
  =
  2 * (1 - 1/(n + 2))

%----------------------------------------
%% The infinite sum of the reciprocal of
%  triangle numbers converges to 2
%----------------------------------------

sum_rTri: THEOREM
  convergent?(series(rTri))
  AND
  inf_sum(rTri)=2
  
  %~~~  The  End  ~~~%
  END triangle_numbers
