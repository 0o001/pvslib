termination[T:TYPE] : THEORY
BEGIN
  IMPORTING well_foundedness[T]  
  Relation : TYPE = PRED[[T,T]]

  termination_by(<:(well_founded?[T]))(R:Relation) : bool =
    FORALL (t1,t2:T): R(t1,t2) IMPLIES t2 < t1

  % Relation is terminating
  terminating(R:Relation) : bool =
    EXISTS (<:(well_founded?[T])) : termination_by(<)(R)

  terminating_no_ascending_seq : THEOREM
    FORALL (R:Relation):
      terminating(R) IFF empty?[sequence[T]](ascending?(R))

  non_terminating(R:Relation) : MACRO bool =
    NOT terminating(R)

  loop_is_not_terminating : LEMMA
    FORALL (R:Relation):
    (EXISTS (t:T) : TC(R)(t,t)) IMPLIES
    non_terminating(R)

  normal(R:Relation)(t:T) : bool =
    NOT EXISTS (tt:T) : R(t,tt)

  normal_form : THEOREM
    FORALL (R:(terminating))(t:T):
      normal(R)(t) OR EXISTS (nft:(normal(R))):TC(R)(t,nft)
  
END termination
