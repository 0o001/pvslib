termination[T:TYPE] : THEORY
BEGIN
  IMPORTING well_foundedness[T]  
  Relation : TYPE = PRED[[T,T]]

  termination_by_wf(R:Relation)(<:(well_founded?[T])): bool =
    FORALL (t1,t2:T): R(t1,t2) IMPLIES t2 < t1

  % Relation is terminating
  terminating(R:Relation) : bool =
    EXISTS (<:(well_founded?[T])) : termination_by_wf(R)(<)

  terminating_no_ascending_seq : THEOREM
    FORALL (R:Relation):
      terminating(R) IFF empty?[sequence[T]](ascending?(R))

  non_terminating(R:Relation) : MACRO bool =
    NOT terminating(R)

  loop_is_not_terminating : LEMMA
    FORALL (R:Relation):
    (EXISTS (t:T) : TC(R)(t,t)) IMPLIES
    non_terminating(R)

  normal(R:Relation)(t:T) : bool =
    NOT EXISTS (tt:T) : R(t,tt)

  normalizable(R:Relation)(t:T) : bool =
    normal(R)(t) OR EXISTS (nft:(normal(R))):TC(R)(t,nft)

  weakly_terminating(R:Relation): bool =
    FORALL (t:T): normalizable(R)(t)  

  terminating_is_weakly_terminating : THEOREM
    FORALL (R:(terminating)): weakly_terminating(R)
  
END termination
