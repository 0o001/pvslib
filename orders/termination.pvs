termination[T:TYPE] : THEORY
BEGIN
  IMPORTING well_foundedness[T]  
  Relation : TYPE = PRED[[T,T]]

  termination_by(<:(well_founded?[T]))(R:Relation) : bool =
    FORALL (t1,t2:T): R(t1,t2) IMPLIES t2 < t1

  terminating(R:Relation) : bool =
    EXISTS (<:(well_founded?[T])) : termination_by(<)(R)

  termination_no_ascending_seq : LEMMA
    FORALL (R:Relation):
      terminating(R) IFF empty?[sequence[T]](ascending?(R))
      
  termination_of_tc : LEMMA
    FORALL (<:(well_founded_order?[T]))(R:Relation) :
      (FORALL (t1,t2:T): R(t1,t2) IMPLIES t2 < t1) IMPLIES
      termination_by(<)(TC(R))

  loop_is_not_terminating : LEMMA
    FORALL (R:Relation):
    (EXISTS (t:T) : TC(R)(t,t)) IMPLIES
    NOT terminating(R) 

END termination
