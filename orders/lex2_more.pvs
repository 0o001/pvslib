lex2_more[T:TYPE] : THEORY
BEGIN
  IMPORTING well_foundedness[T]
  MeasureFun : TYPE = [T->nat]
 
  lt(m1,m2:MeasureFun)(t1,t2:T) : bool = 
    lex2(m1(t1),m2(t1)) < lex2(m1(t2),m2(t2))

  lt_wfo : JUDGEMENT
    lt(m1,m2:MeasureFun) HAS_TYPE (well_founded_order?)

  lt_lex2(m:MeasureFun,wf:(well_founded?))(t1,t2:T): bool =
    m(t1) < m(t2) OR (m(t1) = m(t2) AND wf(t1,t2))

  lt_lex2_wf: JUDGEMENT
    lt_lex2(m:MeasureFun,wf:(well_founded?)) HAS_TYPE (well_founded?[])

  wf_lex2(wf1:{w:[[T,T] -> bool] | well_founded?(w) AND dichotomous?(w)},
    wf2:(well_founded?))(t1,t2:T): bool =
    wf1(t1,t2) OR (NOT wf1(t2,t1) AND wf2(t1,t2))

  wf_lex2_wf: JUDGEMENT
    wf_lex2(wf1:{w:PRED[[T,T]] | well_founded?(w) AND dichotomous?(w)},
    wf2:(well_founded?)) HAS_TYPE (well_founded?[])
  
END lex2_more
