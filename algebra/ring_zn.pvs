%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brasil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brasil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Goiás - Brasil
%%
%%	        André Camapum Carvalho de Freitas
%% 		Universidade Federal de Goiás - Brasil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brasil
%%
%%----------------------------------------------------------------------------
%%
%%	Formalization of results about the ring of integers and
%%	the quotient ring Z/nZ
%%
%%	Last version: June 25, 2020
%%
%%----------------------------------------------------------------------------

ring_zn: THEORY
  BEGIN

   IMPORTING ring_with_one_def,
   	     quotient_rings_with_one[int,+,*,0,1],
	     prop_primes_extra

            d,n,m: VAR posnat
      a,b,c,x,y,k: VAR int
           fs,fsn: VAR finseq[posnat]
	      fsz: VAR finseq[nzint]

%%----------------------------------------------------------------------------

  nZ(n): set[int] = {x | EXISTS k: x=k*n}

  nZ_add(n)(x,y:(nZ(n))): (nZ(n)) = x + y

  fsnZ(fsn): finseq[set[int]] =
       (# length := length(fsn),
             seq := (LAMBDA (i:below[length(fsn)]): nZ(fsn(i))) #)

%%----------------------------------------------------------------------------
 
  Z_ring: LEMMA ring?[int,+,*,0](fullset[int]);

  Z: ring[int,+,*,0] = fullset[int]

  Z1_is_Z: LEMMA nZ(1) = Z

  Z_commutative_ring_w_one: LEMMA commutative_ring_with_one?[int,+,*,0,1](Z)

  nZ_ideal: LEMMA FORALL n: ideal?(nZ(n),Z)

  Z(n): setofsets[int] = Z/nZ(n)

  Zn_finite_set: LEMMA is_finite(Z(n))

  Zn_card_n: LEMMA card(Z(n)) = n

  Zn_commutative_ring_w_one: LEMMA
    commutative_ring_with_one?
         [coset(Z,nZ(n)),add(Z,nZ(n)),product(Z,nZ(n)),nZ(n),+(1,nZ(n))]
	 (Z(n))

  equal_cosets_div: LEMMA a + nZ(n) = b + nZ(n) IFF divides(n,b-a)

  nZ_mZ_sum: LEMMA sum(nZ(n),nZ(m)) = nZ(gcd(n,m))

  nZ_mZ_intersection: LEMMA LET lcm = n*m/gcd(n,m) IN
      intersection(nZ(n),nZ(m)) = nZ(lcm)

  nZ_mZ_rel_prime_intersection: COROLLARY
    rel_prime(n,m) IMPLIES intersection(nZ(n),nZ(m)) = nZ(n*m)

  Z2_charac: LEMMA Z(2) = {y: setof[int] | y = +(0, nZ(2)) OR y = +(1, nZ(2))}
  
  %Zn_charac: CONJECTURE
  %      FORALL(n:posnat): Z(n) = {y: setof[int] | EXISTS (a:below[n]) : y = +(a,nZ(n))}


END ring_zn
