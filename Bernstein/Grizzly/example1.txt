;
; Step by step example with comments
;

show "";
show "*** First, we declare variables x and y in the intervals [-5,5], [0,1], respectively:";
var x in [-5,5];
var y in [0,1];

show "";
show "*** Then, we define a polynomial P:";
poly P = 4*x^2 - (21/10)*x^4 + (1/3)*x^6 + x*y - 4*y^2 + 4*y^4;

show "";
show "*** Now, we check a property on all elements of the range:";
check all P >= -1.1;

show "";
show "*** Now, we check a property on some elements of the range:";
check some P <= -1;

show "";
show "*** We set a small precision for min/max:";
set precision = 0.0001;

show "";
show "*** We also set the display format to rational numbers:";
set format=rational;

show "";
show "*** Now, we compute min and max of P within the given precision:";
minmax P;

show "";
show "*** Finally, we define a region R:";
region R = P >= 0 and P <= 1;

show "";
show "*** We set a precision for paving R with boxes:";
set precision = 0.1;

show "";
show "*** and we pave R (this step may take a few minutes):";
boxes R;
exit;
