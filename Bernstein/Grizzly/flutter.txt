; Flutter example: s^4+c1*s^3+c2s^2+c3*s+c4=0
; Using Routh Stability Criterion, we get the following conditions for stability:
;
; c1>0
; c4>0
; (c1*c2-c3)/c1 >0
; (c1*c2*c3-c3^2-c1^2*c4)/(c1*c2-c3) >0
; 
; Define
; x1=U  (Free stream velocity)
; x2=Ka (Torsional stiffness)
; x3=Kk (Bending stiffness)
; x4=Cl (lift slope curve)

; First we declare the variables
var x1 in [120,170];
var x2 in [85000,115000];
var x3 in [570000,630000];
var x4 in [5,7.5];

; Define the ci's coefficients in terms of xi's
;
poly c1=245*x4*x1/288;
poly c2=(1600*x2 + 160*x3 - 343*x4*x1^2)/144;
poly c3=245*x4*x1*x2/36;
poly c4=5*x3*(320*x2-49*x4*x1^2)/144;

; Then we declare the polynomials
poly g_1 = c1;
poly g_2 = c4;
poly g_3 = c1*c2-c3;
poly g_4 = c1*c2*c3-c3^2-c1^2*c4;

; Finally we declare the safety region
region S = g_1 > 0 and g_2 > 0 and g_3 > 0 and g_4 >0; 

; Set precision
set precision = 0.3;

; The following command computes boxes inside S, outside S, and unknown boxes.
show "";
show "*** Paving region S. This step may take a few minutes.";
boxes S;

exit;
