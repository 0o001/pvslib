bernstein_minmax : THEORY
BEGIN

  IMPORTING Bernstein@strategies

  x,x1,x2,x3,x4,x5,x6,x7,x8:VAR real

  %% The expression (! -1 l) denotes the left hand side of formula 1
  %|- *_minmax : PROOF (then (skeep) (bernstein-minmax (! 1 l)) (assert)) QED

  % Heart dipole
  Heart(x1,x2,x3,x4,x5,x6,x7,x8): MACRO real = 
    -x1*x6^3+3*x1*x6*x7^2-x3*x7^3+3*x3*x7*x6^2-x2*x5^3+3*x2*x5*x8^2-x4*x8^3+3*x4*x8*x5^2-0.9563453

  Heart_minmax: LEMMA 
    -0.1 <= x1 AND x1 <= 0.4 AND 
    0.4 <= x2  AND x2 <= 1 AND 
    -0.7 <= x3 AND x3 <= -0.4 AND 
    -0.7 <= x4 AND x4 <= 0.4 AND 
    0.1 <= x5  AND x5 <= 0.2 AND
    -0.1 <= x6 AND x6 <= 0.2 AND 
    -0.3 <= x7 AND x7 <= 1.1 AND 
    -1.1 <= x8 AND x8 <= -0.3 IMPLIES 
    Heart(x1,x2,x3,x4,x5,x6,x7,x8) >= -1.747 

  Butcher(x1,x2,x3,x4,x5,x6): MACRO real = 
    x6*x2^2 + x5*x3^2-x1*x4^2+x4^3+x4^2-(1/3)*x1+(4/3)*x4

  Butcher_minmax: LEMMA
      -1<=x1 AND x1<=0 AND -0.1<=x2 AND x2<=0.9 AND -0.1<=x3 AND x3<=0.5 AND 
      -1<=x4 AND x4<=-0.1 AND -0.1<=x5 AND x5<=-0.05 AND -0.1<=x6 AND x6<=-0.03
      IMPLIES
      Butcher(x1,x2,x3,x4,x5,x6) >= -1.44 

  Trid(x1,x2,x3,x4): MACRO real = 
    (x1-1)^2 + (x2-1)^2 + (x3-1)^2 + (x4-1)^2 -x1*x2-x2*x3-x3*x4

  Trid_minmax : LEMMA
    -16<=x1 AND x1<=16 AND -16<=x2 AND x2<=16 AND -16<=x3 AND x3<=16 AND -16<=x4 AND
    x4<=16 IMPLIES
    Trid(x1,x2,x3,x4) >= -16.01

  AdaptiveLV(x1,x2,x3,x4): MACRO real = 
    x1*x2^2+x1*x3^2+x1*x4^2-1.1*x1+1

  AdaptiveLV_minmax: LEMMA
    -2<=x1 AND x1<=2 AND -2<=x2 AND x2<=2 AND -2<=x3 AND x3<=2 AND -2<=x4 AND x4<=2 IMPLIES
    AdaptiveLV(x1,x2,x3,x4) >= -20.801

  Caprasse(x1,x2,x3,x4): MACRO real = 
    -x1*x3^3+4*x2*x3^2*x4+4*x1*x3*x4^2+2*x2*x4^3+4*x1*x3+4*x3^2-10*x2*x4-10*x4^2+2

  Caprasse_minmax: LEMMA
    -0.5<=x1 AND x1<=0.5 AND -0.5<=x2 AND x2<=0.5 AND -0.5<=x3 AND 
    x3<=0.5 AND -0.5<=x4 AND x4<=0.5 IMPLIES
      Caprasse(x1,x2,x3,x4) >= -3.188

  % The answer in the paper for the Schwefel function is also incorrect. It is actually zero.

  Schwefel(x1,x2,x3): MACRO real = 
    (x1-x2^2)^2+(x2-1)^2+(x1-x3^2)^2+(x3-1)^2

  Schwefel_minmax: LEMMA
    -10<=x1 AND x1<=10 AND -10<=x2 AND x2<=10 AND -10<=x3 AND x3<=10 IMPLIES
    Schwefel(x1,x2,x3) >= -0.007

END bernstein_minmax
