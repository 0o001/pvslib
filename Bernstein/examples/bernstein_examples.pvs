%% Examples in this files are taken from several sources, including
%%  Global Optimization in Type Theory, Roland Zumkeller, PhD thesis.
%%  Andrew Smith, University of Applied Sciences/FH Konstanz, Germany
%%  Luis Crespo, National Institute of Aerospace, USA

bernstein_examples : THEORY
BEGIN

  IMPORTING Bernstein@strategies

  x,y,z: VAR real
  x1,x2,x3,x4,x5,x6 : VAR real

  %|- *_ : PROOF (bernstein) QED

  test_forall_: LEMMA
    FORALL(x,y:real):
      -0.5 <= x AND x <= 1 AND -2 <= y AND y <= 1 IMPLIES 
      4*x^2 - (21/10)*x^4 + (1/3)*x^6 + (x-3)*y - 4*y^2 + 4*y^4 > -3.4

  test_exists_ : LEMMA
    EXISTS (x,y:real):
      -0.5 <= x AND x <= 1 AND -2 <= y AND y <= 1 AND
      4*x^2 - (21/10)*x^4 + (1/3)*x^6 + (x-3)*y - 4*y^2 + 4*y^4 < -3.39

  test_exists_open_ : LEMMA
    EXISTS (x:real): -1 < x AND x < 1 AND
      x^2-(2/3)*x+999/9000<0

  test_forall_unbounded_ : LEMMA
    FORALL (x:real): x >= 1 IMPLIES x^2 - x >= 0

  % The following lemma requires a deeper search
  test_exists_unbounded_200 : LEMMA 
    EXISTS (x:real): 
      x >= 3 AND 
      x^4-62222213381381813820*x^3 +
      1451851439279328769920320712237428622150*x^2 -
      15056235008817449421423031381578393401489769393058404685500*x + 
      58552016714929402061218323458667521515902447404840532442469619228223540850625 <= 
      1/20000000 
  %|- test_exists_unbounded_200 : PROOF
  %|- (bernstein :depth 200)
  %|- QED 

  zumkeller1a_ : LEMMA
    abs(x) <= 5 AND abs(y) <= 5 IMPLIES 
      4*x^2 - (21/10) * x^4 + (1/3) * x^6 + x*y - 4*y^2 + 4*y^4 >= -13941776395527295/13510798882111488

  zumkeller1b_ : LEMMA
    EXISTS (x,y) : abs(x) <= 5 AND abs(y) <= 5 AND
      4*x^2 - (21/10) * x^4 + (1/3) * x^6 + x*y - 4*y^2 + 4*y^4 >= -13941776395527295/13510798882111488+0.001

  zumkeller2_: LEMMA 
    abs(x) <= 2 AND abs(y) <= 2 IMPLIES 
      100*(y-x^2)^2+(1-x)^2 >= -42601/50331648

  smith_ : LEMMA
    1<=x1 AND x1<=2 AND 2<=x2 AND x2<=3 AND 4<=x3 AND x3<=6 AND -5<=x4 AND x4<=2
    AND 2<=x5 AND x5<=10 IMPLIES
      3*x1^2*x2^3*x3^4+x1^3*x2*x3^4-5*x1*x2*x4^5 + x3*x4*x5^3 <= 1000000

  beale1a_: LEMMA 
    -10<=x AND x<=10 AND -10<=y AND y<=10 IMPLIES
      (3/2-x+x*y)^2+(9/4-x+x*y^2)^2 + (21/8-x+x*y^3)^2 >=-460353137725297 / 576460752303423488 

  beale1b_: LEMMA 
    EXISTS (x,y): -10<=x AND x<=10 AND -10<=y AND y<=10 AND
      (3/2-x+x*y)^2+(9/4-x+x*y^2)^2 + (21/8-x+x*y^3)^2 <= 41959384327089/288230376151711744

  crespo1a_ : LEMMA
    abs(x) <= 0.1969 AND abs(y) <= 1.3786 IMPLIES
      2*y*x^5 + y^2*x^4 - y^3*x^2 - 2*y^3*x + 0.5*x*y < 1

  crespo1b_ : LEMMA
    EXISTS (x,y) : abs(x) <= 1 AND abs(y) <= 2 AND
      2*y*x^5 + y^2*x^4 - y^3*x^2 - 2*y^3*x + 0.5*x*y <= 1

  crespo2a_ : LEMMA
    abs(x) <= 1.2 AND abs(y) <= 0.6 AND abs(z) <= 0.3 IMPLIES 
      2*y*x^5 + y^2*x^4 - y^3*x^2 - 2*y^3*x + 0.5*x*y - 10*z^2 + 10*z <= 6

  crespo2b_ : LEMMA
    EXISTS (x,y,z) : abs(x) <= 1.22 AND abs(y) <= 0.6132 AND abs(z) <= 0.3066 AND
      2*y*x^5 + y^2*x^4 - y^3*x^2 - 2*y^3*x + 0.5*x*y - 10*z^2 + 10*z >= 6

  crespo3a_ : LEMMA
    abs(x) <= 0.5055 AND abs(y) <= 1.2637 AND abs(z) <= 0.2527 IMPLIES 
      -y^3*x^2 - 9*y*x^3 - 4*y^2*x^3 - y + 2*x + 3*y^3 - 6 - 2*z - z^2 <= 0

  crespo3b_ : LEMMA
    EXISTS (x,y,z) : abs(x) <= 0.5222 AND abs(y) <= 1.3054 AND abs(z) <= 0.2611 AND
      -y^3*x^2 - 9*y*x^3 - 4*y^2*x^3 - y + 2*x + 3*y^3 - 6 - 2*z - z^2 <= 0

  % This answer appears to be incorrect in the paper

  Butcher(x1,x2,x3,x4,x5,x6): MACRO real = 
    x6*x2^2 + x5*x3^2-x1*x4^2+x4^3+x4^2-(1/3)*x1+(4/3)*x4

  Butcher_forall_: LEMMA
      -1<=x1 AND x1<=0 AND -0.1<=x2 AND x2<=0.9 AND -0.1<=x3 AND x3<=0.5 AND 
      -1<=x4 AND x4<=-0.1 AND -0.1<=x5 AND x5<=-0.05 AND -0.1<=x6 AND x6<=-0.03
      IMPLIES
      Butcher(x1,x2,x3,x4,x5,x6) >= -1.44 %-0.21901

  Butcher_exists_: LEMMA
    EXISTS (x1,x2,x3,x4,x5,x6):
      -1<=x1 AND x1<=0 AND -0.1<=x2 AND x2<=0.9 AnD -0.1<=x3 AND x3<=0.5 AND 
      -1<=x4 AND x4<=-0.1 AND -0.1<=x5 AND x5<=-0.05 AND -0.1<=x6 AND
      x6<=-0.03 AND
      Butcher(x1,x2,x3,x4,x5,x6) <= -0.21899

  AdaptiveLV(x1,x2,x3,x4): MACRO real = 
    x1*x2^2+x1*x3^2+x1*x4^2-1.1*x1+1

  AdaptiveLV_forall_: LEMMA
    -2<=x1 AND x1<=2 AND -2<=x2 AND x2<=2 AND -2<=x3 AND x3<=2 AND -2<=x4 AND x4<=2
    IMPLIES
    AdaptiveLV(x1,x2,x3,x4) >= -20.801

  AdaptiveLV_exists_: LEMMA
    EXISTS (x1,x2,x3,x4):
    -2<=x1 AND x1<=2 AND -2<=x2 AND x2<=2 AND -2<=x3 AND x3<=2 AND 
    -2<=x4 AND x4<=2 AND
      AdaptiveLV(x1,x2,x3,x4) <= -20.799

  ReactionDiffusion(x1,x2,x3): MACRO real = 
    -x1+2*x2-x3-0.835634534*x2*(1+x2)

  ReactionDiffusion_forall_: LEMMA
    -5<=x1 AND x1<=5 AND -5<=x2 AND x2<=5 AND -5<=x3 AND x3<=5 IMPLIES
    ReactionDiffusion(x1,x2,x3) >= -36.7126907 % Paper answer is -10.4057

  ReactionDiffusion_exists_: LEMMA
    EXISTS (x1,x2,x3):
    -5<=x1 AND x1<=5 AND -5<=x2 AND x2<=5 AND -5<=x3 AND x3<=5 AND
    ReactionDiffusion(x1,x2,x3) <= -36.7126

  Chebyshev1_ : LEMMA
    abs(x) <= 1 IMPLIES (2*x^2 -1)^2 <= 1

  Chebyshev2_ : LEMMA
    abs(x) <= 1 IMPLIES (4*x^3 -3*x)^2 <= 1


END bernstein_examples
