foursquare: THEORY

% Every natural number is a sum of four squares
% Lagrange proved this in 1770
% Author: Narkawicz

BEGIN

IMPORTING ints@primes,ints@pigeonhole,ints@mod_lems,
	  reals@sq

n,m:VAR nat
p:VAR (prime?)
pn,pm:VAR posnat

i1,i2,i3,i4,
j1,j2,j3,j4:VAR int

e,f:VAR int

a,b,c,d:VAR nat

mod_eq: LEMMA mod(e,pn)=mod(f,pn) IFF
	      divides(pn,e-f)

Euler_four_square: LEMMA
  (i1^2+i2^2+i3^2+i4^2)*(j1^2+j2^2+j3^2+j4^2)=
  (i1*j1 + i2*j2 + i3*j3 + i4*j4)^2 +
  (i1*j2 - i2*j1 + i3*j4 - i4*j3)^2 +
  (i1*j3 - i2*j4 - i3*j1 + i4*j2)^2 +
  (i1*j4 + i2*j3 - i3*j2 - i4*j1)^2

sum_four_squares?(n): bool =
  EXISTS (a,b,c,d): n=a^2+b^2+c^2+d^2

sum_four_squares_int?(e):bool =
  EXISTS (i1,i2,i3,i4):
  e = i1^2+i2^2+i3^2+i4^2

sum_four_squares_int_def: LEMMA
  sum_four_squares?(n) IFF
  sum_four_squares_int?(n)

sum_four_squares_reduces: LEMMA
  (FORALL p: p>2 IMPLIES sum_four_squares?(p))
  IMPLIES
  (FORALL n: sum_four_squares?(n))

square_residues_distinct: LEMMA
  a<=(p-1)/2 AND b<=(p-1)/2 AND a/=b IMPLIES
  mod(a^2,p)/=mod(b^2,p)

sum_four_squares_pigeonhole: LEMMA
  p>2 IMPLIES
  EXISTS (a,b): a<=(p-1)/2 AND b<=(p-1)/2 
  AND mod(a^2,p)=mod(-b^2-1,p)

min_mult_sum_four_squares?(p,pm): bool = 
  pm<p AND sum_four_squares?(pm*p) AND
  	       FORALL (pn): pn<pm IMPLIES
	       NOT sum_four_squares?(pn*p)

mult_is_sum_four_squares: LEMMA 
  EXISTS (pm): min_mult_sum_four_squares?(p,pm)

nat_is_sum_four_squares: LEMMA
  sum_four_squares?(n)

sum_four_squares: LEMMA
  EXISTS (a,b,c,d): n=a^2+b^2+c^2+d^2


END foursquare
