%% interval_examples4Q.pvs
%% 
%% These examples only deal with rational expressions, for that reason the theory 
%% just imports strategies4Q. 
%%  

interval_examples4Q : theory
BEGIN

  IMPORTING interval_arith@strategies4Q

  x,y,z : var real

  %%%% Examples of numerical

  zero_to_one_quarter : LEMMA
    x ## [|0,1|] IMPLIES
    x*(1-x) ## [|0.0000, 0.2501|]
%% The expression (! 1 1) refers to formula 1, 1st term, i.e., x*(1-x)
%|- zero_to_one_quarter : PROOF
%|- (then (skeep) (numerical (! 1 1) :precision 4 :maxdepth 20 :verbose? t))
%|- QED

  x0,x1,x2,x3,x4,x5,x6,x7 : VAR real

  Heart(x0,x1,x2,x3,x4,x5,x6,x7): MACRO real = 
    -x0*x5^3+3*x0*x5*x6^2-x2*x6^3+3*x2*x6*x5^2-x1*x4^3+3*x1*x4*x7^2-x3*x7^3+3*x3*x7*x4^2-0.9563453

  hdp_mm: LEMMA 
    -0.1 <= x0 AND x0 <= 0.4 AND 
     0.4 <= x1  AND x1 <= 1 AND 
    -0.7 <= x2 AND x2 <= -0.4 AND 
    -0.7 <= x3 AND x3 <= 0.4 AND 
     0.1 <= x4  AND x4 <= 0.2 AND
    -0.1 <= x5 AND x5 <= 0.2 AND 
    -0.3 <= x6 AND x6 <= 1.1 AND 
    -1.1 <= x7 AND x7 <= -0.3 IMPLIES 
     Heart(x0,x1,x2,x3,x4,x5,x6,x7) ## [|-1.852, 1.518|]

%% The expression (! 1 1) refers to formula 1, 1st term, i.e., polynomial Heart
%|- hdp_mm : PROOF
%|- (then (skeep) (numerical (! 1 1) :verbose? t))
%|- QED

  %%%% Examples of interval

  hdp_minmax: LEMMA 
    -0.1 <= x0 AND x0 <= 0.4 AND 
     0.4 <= x1  AND x1 <= 1 AND 
    -0.7 <= x2 AND x2 <= -0.4 AND 
    -0.7 <= x3 AND x3 <= 0.4 AND 
     0.1 <= x4  AND x4 <= 0.2 AND
    -0.1 <= x5 AND x5 <= 0.2 AND 
    -0.3 <= x6 AND x6 <= 1.1 AND 
    -1.1 <= x7 AND x7 <= -0.3 IMPLIES 
     Heart(x0,x1,x2,x3,x4,x5,x6,x7) ## [| -1.76, 1.46 |]

%|- hdp_minmax : PROOF
%|- (then (skeep) (interval))
%|- QED

  common_point: LEMMA 
    EXISTS (x,y,z:real): abs(x) <= 10 AND abs(y) <= 10 AND z ## [|0,1/2|] AND 
      LET x2 = x^2,
          y2 = y^2 IN
        x2-2*x+1+y2-2*y+1<1 AND x2 + y2 < 3-2*y+0.01

%|- common_point : PROOF (interval :verbose? t) QED

   Eps : posreal = 0.0001

   simple_ite : LEMMA
     FORALL (x:real | x ## [| 0, 10 |]) :
       IF x <= 1 THEN sq(x) <= x+Eps
       ELSE sq(x) >= x-Eps
       ENDIF

%|- simple_ite : PROOF
%|- (interval)
%|- QED

  zumkeller1b_ : LEMMA
    EXISTS (x,y) : abs(x) <= 5 AND abs(y) <= 5 AND
      4*x^2 - (21/10) * x^4 + (1/3) * x^6 + x*y - 4*y^2 + 4*y^4 >= -13941776395527295/13510798882111488+0.001

%|- zumkeller1b_ : PROOF
%|- (interval)
%|- QED

  zumkeller2_: LEMMA 
    abs(x) <= 2 AND abs(y) <= 2 IMPLIES 
      100*(y-x^2)^2+(1-x)^2 >= -42601/50331648

%|- zumkeller2_ : PROOF
%|- (interval)
%|- QED

  smith_ : LEMMA
    1<=x1 AND x1<=2 AND 2<=x2 AND x2<=3 AND 4<=x3 AND x3<=6 AND -5<=x4 AND x4<=2
    AND 2<=x5 AND x5<=10 IMPLIES
      3*x1^2*x2^3*x3^4+x1^3*x2*x3^4-5*x1*x2*x4^5 + x3*x4*x5^3 <= 1000000

%|- smith_ : PROOF
%|- (interval)
%|- QED

  beale1a_: LEMMA 
    -10<=x AND x<=10 AND -10<=y AND y<=10 IMPLIES
      (3/2-x+x*y)^2+(9/4-x+x*y^2)^2 + (21/8-x+x*y^3)^2 >=-460353137725297 / 576460752303423488 

%|- beale1a_ : PROOF
%|- (interval)
%|- QED

  beale1b_: LEMMA 
    EXISTS (x,y): -10<=x AND x<=10 AND -10<=y AND y<=10 AND
      (3/2-x+x*y)^2+(9/4-x+x*y^2)^2 + (21/8-x+x*y^3)^2 <= 41959384327089/288230376151711744
%|- beale1b_ : PROOF
%|- (interval :maxdepth 17)
%|- QED

  crespo1a_ : LEMMA
    abs(x) <= 0.1969 AND abs(y) <= 1.3786 IMPLIES
      2*y*x^5 + y^2*x^4 - y^3*x^2 - 2*y^3*x + 0.5*x*y < 1

%|- crespo1a_ : PROOF
%|- (interval)
%|- QED

  crespo1b_ : LEMMA
    EXISTS (x,y) : abs(x) <= 1 AND abs(y) <= 2 AND
      2*y*x^5 + y^2*x^4 - y^3*x^2 - 2*y^3*x + 0.5*x*y <= 1

%|- crespo1b_ : PROOF
%|- (interval)
%|- QED

  crespo2a_ : LEMMA
    abs(x) <= 1.2 AND abs(y) <= 0.6 AND abs(z) <= 0.3 IMPLIES 
      2*y*x^5 + y^2*x^4 - y^3*x^2 - 2*y^3*x + 0.5*x*y - 10*z^2 + 10*z <= 6

%|- crespo2a_ : PROOF
%|- (interval)
%|- QED

  crespo2b_ : LEMMA
    EXISTS (x,y,z) : abs(x) <= 1.22 AND abs(y) <= 0.6132 AND abs(z) <= 0.3066 AND
      2*y*x^5 + y^2*x^4 - y^3*x^2 - 2*y^3*x + 0.5*x*y - 10*z^2 + 10*z >= 6

%|- crespo2b_ : PROOF
%|- (interval :maxdepth 17)
%|- QED

  crespo3a_ : LEMMA
    abs(x) <= 0.5055 AND abs(y) <= 1.2637 AND abs(z) <= 0.2527 IMPLIES 
      -y^3*x^2 - 9*y*x^3 - 4*y^2*x^3 - y + 2*x + 3*y^3 - 6 - 2*z - z^2 <= 0

%|- crespo3a_ : PROOF
%|- (interval)
%|- QED

  crespo3b_ : LEMMA
    EXISTS (x,y,z) : abs(x) <= 0.5222 AND abs(y) <= 1.3054 AND abs(z) <= 0.2611 AND
      -y^3*x^2 - 9*y*x^3 - 4*y^2*x^3 - y + 2*x + 3*y^3 - 6 - 2*z - z^2 <= 0

%|- crespo3b_ : PROOF
%|- (interval)
%|- QED

  Magnetism(x1,x2,x3,x4,x5,x6,x7): MACRO real =
      x1^2+2*x2^2+2*x3^2+2*x4^2+2*x5^2+2*x6^2+2*x7^2-x1

  Magnetism_exists_: LEMMA
      EXISTS (x1,x2,x3,x4,x5,x6,x7): 
      x1<=1 AND x2<=1 AND x3<=1 AND x4<=1 AND x5<=1 AND x6<=1 AND x7<=1 AND
      -1<=x1 AND -1<=x2 AND -1<=x3 AND -1<=x4 AND -1<=x5 AND -1<=x6 AND -1<=x7
      AND
      Magnetism(x1,x2,x3,x4,x5,x6,x7) <= -0.2499
%|- Magnetism_exists_ : PROOF (interval) QED

  Butcher(x1,x2,x3,x4,x5,x6): MACRO real = 
    x6*x2^2 + x5*x3^2-x1*x4^2+x4^3+x4^2-(1/3)*x1+(4/3)*x4

  Butcher_exists_: LEMMA
    EXISTS (x1,x2,x3,x4,x5,x6):
      -1<=x1 AND x1<=0 AND -0.1<=x2 AND x2<=0.9 AnD -0.1<=x3 AND x3<=0.5 AND 
      -1<=x4 AND x4<=-0.1 AND -0.1<=x5 AND x5<=-0.05 AND -0.1<=x6 AND x6<=-0.03 AND
      Butcher(x1,x2,x3,x4,x5,x6) <= -0.21899
%|- Butcher_exists_ : PROOF (interval) QED

  AdaptiveLV(x1,x2,x3,x4): MACRO real = 
    x1*x2^2+x1*x3^2+x1*x4^2-1.1*x1+1

  AdaptiveLV_forall_: LEMMA
    -2<=x1 AND x1<=2 AND -2<=x2 AND x2<=2 AND -2<=x3 AND x3<=2 AND -2<=x4 AND x4<=2 IMPLIES
    AdaptiveLV(x1,x2,x3,x4) >= -20.801
%|- AdaptiveLV_forall_ : PROOF (interval) QED

  Caprasse(x1,x2,x3,x4): MACRO real = 
    -x1*x3^3+4*x2*x3^2*x4+4*x1*x3*x4^2+2*x2*x4^3+4*x1*x3+4*x3^2-10*x2*x4-10*x4^2+2

  Caprasse_exists_: LEMMA
    EXISTS (x1,x2,x3,x4):
    -2<=x1 AND x1<=2 AND -2<=x2 AND x2<=2 AND -2<=x3 AND x3<=2 AND -2<=x4 AND x4<=2 AND
      Caprasse(x1,x2,x3,x4) <= -3.18009
%|- Caprasse_exists_ : PROOF (interval) QED

  Schwefel(x1,x2,x3): MACRO real = 
    (x1-x2^2)^2+(x2-1)^2+(x1-x3^2)^2+(x3-1)^2

  Schwefel_forall_: LEMMA
    -10<=x1 AND x1<=10 AND -10<=x2 AND x2<=10 AND -10<=x3 AND x3<=10 IMPLIES
    Schwefel(x1,x2,x3) >= -0.00000000058806
%|- Schwefel_forall_ : PROOF (interval) QED

  ReactionDiffusion(x1,x2,x3): MACRO real = 
    -x1+2*x2-x3-0.835634534*x2*(1+x2)

  ReactionDiffusion_forall_: LEMMA
    -5<=x1 AND x1<=5 AND -5<=x2 AND x2<=5 AND -5<=x3 AND x3<=5 IMPLIES
    ReactionDiffusion(x1,x2,x3) >= -36.7126907 % Paper answer is -10.4057
%|- ReactionDiffusion_forall_ : PROOF (interval) QED

  Chebyshev1 : LEMMA
    abs(x) <= 1 IMPLIES (2*x^2 -1)^2 <= 1
%|- Chebyshev1 : PROOF (interval) QED

  example_5: LEMMA 
    x<0 AND x>-5 IMPLIES x^5-2*x^3 < 6
%|- example_5 : PROOF (interval) QED

  example_5_m1000_0: LEMMA 
  x<0 AND x>-1000 IMPLIES x^5-2*x^3 < 6
%|- example_5_m1000_0 : PROOF (interval) QED

  example_5_m800_0: LEMMA 
  x<0 AND x>-800 IMPLIES x^5-2*x^3 < 6
%|- example_5_m800_0 : PROOF (interval) QED

END interval_examples4Q
